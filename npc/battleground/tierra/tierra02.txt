// ===== Script rAthena ===========================================
// = Sistema BattleGround - Tierra Gorge 2
// ===== Descrição: ===============================================
// = [Conversão oficial]
// = Segundo campo de batalha do desfiladeiro Tierra.
// = - Equipe vencedora: 3 emblemas (5 se VIP)
// = - Time perdedor: 1 emblema (3 se VIP)
// ===== Traduzido por: ===============================================
// = Guru
// ===== Changelogs: =============================================== =
// = 1.0 Primeira versão. [L0ne_W0lf]
// = 1.1 'sala de espera' atualizada para suportar o zeny / lvl necessário. [Kisuka]
// = 1.2 Removido o cheque MaxLvl nas salas de espera.
// = 1.3 Padronização de rótulo. [Euphy]
// = 1.4 Adicionada função de gerenciamento GM. [Euphy]
// = 1.5 Adicionados recursos VIP e criada uma função de recompensa. [Euphy]
// = 1.6 Adicionadas verificações de battle_config para permitir que este script use a nova interface de fila ou o método anterior. [Aleos]
// ====================================================== ============

// NPCs da sala de espera
//============================================================
bat_room,114,227,5	script	Tenente Rundel	418,{
	end;

OnInit:
	waitingroom "Estação de Batalha",11,"start#bat_a02::OnReadyCheck",1,0,80;
	end;

OnEnterBG:
	set $@TierraBG2_id1, waitingroom2bg("bat_a02",50,374,"start#bat_a02::OnGuillaumeQuit","");
	end;
}

bat_room,114,204,1	script	Tenente Guerrit	414,{
	end;

OnInit:
	waitingroom "Estação de Batalha",11,"start#bat_a02::OnReadyCheck",1,0,80;
	end;

OnEnterBG:
	set $@TierraBG2_id2, waitingroom2bg("bat_a02",42,16,"start#bat_a02::OnCroixQuit","");
	end;
}

bat_room,1,151,3	script	#bat_a02_timer	844,{
	end;

OnInit:
	if (getbattleflag("feature.bgqueue"))
		end;
OnEnable:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	if (!bg_get_data($@TierraBG2_id1, 0) && !bg_get_data($@TierraBG2_id2, 0)) {
		donpcevent "countdown#bat_a02::OnStop";
		if (getbattleflag("feature.bgqueue"))
			bg_reserve "bat_a02", true;
		mapwarp "bat_a02","bat_room",154,150;
		$@TierraBG2 = 0; $@TierraBG2_Victory = 0;
		if( $@TierraBG2_id1 ) { bg_destroy $@TierraBG2_id1; $@TierraBG2_id1 = 0; }
		if( $@TierraBG2_id2 ) { bg_destroy $@TierraBG2_id2; $@TierraBG2_id2 = 0; }
		if (getbattleflag("feature.bgqueue"))
			bg_unbook "bat_a02";
		end;
	}
	initnpctimer;
	end;
}

bat_a02,352,342,0	script	#bat_a02_quest_a	-1,5,5,{
	end;

OnTouch:
	if (checkquest(2069) < 0)
		setquest 2069;
	end;
}

bat_a02,353,52,0	script	#bat_a02_quest_b	-1,5,5,{
	end;

OnTouch:
	if (checkquest(2069) < 0)
		setquest 2069;
	end;
}

bat_room,113,220,0	warp	back_bgrooma02a	1,1,bat_room,154,150
bat_room,113,211,0	warp	back_bgrooma02b	1,1,bat_room,154,150

// Tierra Gorge Motor de campo de batalha
//============================================================
bat_a02,15,15,3	script	start#bat_a02	844,{
OnInit:
	mapwarp "bat_a02","bat_room",154,150;
	end;

OnEnable:
	donpcevent "OBJ#bat_a02_a::OnKill";
	donpcevent "OBJ#bat_a02_a::OnEnable";
	donpcevent "OBJ#bat_a02_b::OnKill";
	donpcevent "OBJ#bat_a02_b::OnEnable";
	donpcevent "Barricada#bat_a02_a::OnKill";
	donpcevent "Barricada#bat_a02_a::OnEnable";
	donpcevent "Barricada#bat_a02_b::OnKill";
	donpcevent "Barricada#bat_a02_b::OnEnable";
	donpcevent "OBJ#bat_a02_n::OnKill";
	donpcevent "OBJ#bat_a02_n::OnEnable";
	donpcevent "NOBJ_mob#bat_a02_a::OnKill";
	donpcevent "NOBJ_mob#bat_a02_b::OnKill";
	donpcevent "Terapeuta#a02_a::OnEnable";
	donpcevent "Terapeuta#a02_b::OnEnable";
	donpcevent "countdown#bat_a02::OnEnable";
	disablenpc "Ferreiro Guillaume#a02";
	disablenpc "Ferreiro Croix#bat_a02";
	disablenpc "Guillaume Vintenar#a02_a";
	disablenpc "Croix Vintenar#a02_b";
	end;

OnGuillaumeActive:
	warp "bat_a02",352,342;
	end;

OnCroixActive:
	warp "bat_a02",353,52;
	end;

OnGuillaumeQuit:
OnCroixQuit:
	if (getbattleflag("feature.bgqueue"))
		bg_desert;
	else
		bg_leave;
	end;

OnReadyCheck:
	if( $@TierraBG2 )
		end;
	if (!getbattleflag("feature.bgqueue")) {
		set .@Guillaume, getwaitingroomstate(0,"Tenente Rundel");
		set .@Croix, getwaitingroomstate(0,"Tenente Guerrit");
		if( !.@Guillaume && !.@Croix ) {
			donpcevent "#bat_a02_timer::OnStop";
			end;
		}
		else if( .@Guillaume < 10 || .@Croix < 10 )
			end;
	}
	set $@TierraBG2,1;
	if (!getbattleflag("feature.bgqueue")) {
		donpcevent "Tenente Rundel::OnEnterBG";
		donpcevent "Tenente Guerrit::OnEnterBG";
	}
	donpcevent "start#bat_a02::OnEnable";
	bg_warp $@TierraBG2_id1,"bat_a02",352,342;
	bg_warp $@TierraBG2_id2,"bat_a02",353,52;
	initnpctimer;
	end;

OnTimer10000:
	stopnpctimer;
	donpcevent "#bat_a02_timer::OnEnable";
	end;
}

bat_a02,15,16,3	script	OBJ#bat_a02_a	844,{
OnEnable:
	bg_monster $@TierraBG2_id1,"bat_a02",177,345,"Depósito de Comida",1909,"OBJ#bat_a02_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","OBJ#bat_a02_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","OBJ#bat_a02_a::OnMyMobDead") < 1) {
		donpcevent "Terapeuta#a02_a::OnStop";
		donpcevent "Terapeuta#a02_b::OnStop";
		set $@TierraBG2_Victory, 2;
		enablenpc "Guillaume Vintenar#a02_a";
		enablenpc "Croix Vintenar#a02_b";
		mapannounce "bat_a02", "Croix Vintenar Swandery: Destruímos o Armazém de Alimentos de Guillaume. Nós ganhamos isso! Wow!",bc_map,"0xFFCE00";
		bg_warp $@TierraBG2_id1,"bat_a02",50,374;
		bg_warp $@TierraBG2_id2,"bat_a02",42,16;
		if (getbattleflag("feature.bgqueue"))
			bg_reserve "bat_a02", true;
	}
	end;
}

bat_a02,15,17,3	script	OBJ#bat_a02_b	844,{
OnEnable:
	bg_monster $@TierraBG2_id2,"bat_a02",167,50,"Depósito de Comida",1910,"OBJ#bat_a02_b::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","OBJ#bat_a02_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","OBJ#bat_a02_b::OnMyMobDead") < 1) {
		donpcevent "Terapeuta#a02_a::OnStop";
		donpcevent "Terapeuta#a02_b::OnStop";
		set $@TierraBG2_Victory, 1;
		enablenpc "Guillaume Vintenar#a02_a";
		enablenpc "Croix Vintenar#a02_b";
		mapannounce "bat_a02", "Guillaume Vintenar Axl Rose: Destruímos o depósito de alimentos de Croix. Nós ganhamos isso! Wow!",bc_map,"0xFFCE00";
		bg_warp $@TierraBG2_id1,"bat_a02",50,374;
		bg_warp $@TierraBG2_id2,"bat_a02",42,16;
		if (getbattleflag("feature.bgqueue"))
			bg_reserve "bat_a02", true;
	}
	end;
}

bat_a02,15,18,3	script	Barricada#bat_a02_a	844,{
OnEnable:
	for( set .@i,185; .@i < 202; set .@i,.@i+1 )
		bg_monster $@TierraBG2_id1,"bat_a02",.@i,266,"Barricada",1906,"Barricada#bat_a02_a::OnMyMobDead";
	setwall "bat_a02",186,266,16,6,1,"bat_a02_c1";
	end;

OnKill:
	killmonster "bat_a02","Barricada#bat_a02_a::OnMyMobDead";
	if (checkwall("bat_a02_c1") == true)
		delwall "bat_a02_c1";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","Barricada#bat_a02_a::OnMyMobDead") < 17) {
		killmonster "bat_a02","Barricada#bat_a02_a::OnMyMobDead";
		delwall "bat_a02_c1";
		enablenpc "Ferreiro Guillaume#a02";
		mapannounce "bat_a02", "Guillaume Vintenar Axl Rose: A Barricada no vale foi destruída! Onde está o ferreiro? Precisamos reconstruir a Barricada!",bc_map,"0xFFCE00";
	}
	end;
}

bat_a02,15,19,3	script	Barricada#bat_a02_b	844,{
OnEnable:
	for( set .@i,169; .@i < 186; set .@i,.@i+1 )
		bg_monster $@TierraBG2_id2,"bat_a02",.@i,129,"Barricada",1906,"Barricada#bat_a02_b::OnMyMobDead";
	setwall "bat_a02",170,129,16,6,1,"bat_a02_g1";
	end;

OnKill:
	killmonster "bat_a02","Barricada#bat_a02_b::OnMyMobDead";
	if (checkwall("bat_a02_g1") == true)
		delwall "bat_a02_g1";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","Barricada#bat_a02_b::OnMyMobDead") < 17) {
		killmonster "bat_a02","Barricada#bat_a02_b::OnMyMobDead";
		delwall "bat_a02_g1";
		enablenpc "Ferreiro Croix#bat_a02";
		mapannounce "bat_a02", "Croix Vintenar Swandery: A Barricada do vale foi destruída! Onde está o ferreiro? Precisamos reconstruir a Barricada!",bc_map,"0xFFCE00";
	}
	end;
}

bat_a02,15,20,3	script	OBJ#bat_a02_n	844,{
OnEnable:
	monster "bat_a02",273,203,"Bandeira Neutralidade",1911,1,"OBJ#bat_a02_n::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","OBJ#bat_a02_n::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_a02","OBJ#bat_a02_n::OnMyMobDead") < 17) {
		bg_team_setxy getcharid(4),56,212;
		if (getcharid(4) == $@TierraBG2_id1) {
			donpcevent "NOBJ_mob#bat_a02_a::OnEnable";
			mapannounce "bat_a02", "Guillaume capturou uma Bandeira de Neutralidade, então eles têm uma vantagem.",bc_map,"0xFFCE00";
		}
		else {
			donpcevent "NOBJ_mob#bat_a02_b::OnEnable";
			mapannounce "bat_a02", "Croix capturou uma Bandeira de Neutralidade, então eles têm uma vantagem.",bc_map,"0xFFCE00";
		}
	}
	end;
}

bat_a02,15,21,3	script	NOBJ_mob#bat_a02_a	844,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a02_b::OnKill";
	bg_monster $@TierraBG2_id1,"bat_a02",272,204,"Guillaume Camp Guardian",1949,"NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id1,"bat_a02",272,213,"Guillaume Camp Guardian",1949,"NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id1,"bat_a02",273,197,"Guillaume Camp Guardian",1950,"NOBJ_mob#bat_a02_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","NOBJ_mob#bat_a02_a::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a02,15,22,3	script	NOBJ_mob#bat_a02_b	844,{
OnEnable:
	donpcevent "NOBJ_mob#bat_a02_a::OnKill";
	bg_monster $@TierraBG2_id2,"bat_a02",272,204,"Guardião Croix",1949,"NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id2,"bat_a02",272,213,"Guardião Croix",1949,"NOBJ_mob#bat_a02_a::OnMyMobDead";
	bg_monster $@TierraBG2_id2,"bat_a02",273,197,"Guardião Croix",1950,"NOBJ_mob#bat_a02_a::OnMyMobDead";
	end;

OnKill:
	killmonster "bat_a02","NOBJ_mob#bat_a02_b::OnMyMobDead";
	end;

OnMyMobDead:
	end;
}

bat_a02,185,270,1	script	Ferreiro Guillaume#a02	851,{
	if (getcharid(4) == $@TierraBG2_id1) {
		mes "[Ferreiro Guillaume]";
		mes "Estamos com urgência! A Barricada foi destruída!";
		mes "Podemos reparar a Barricada com ^3131FF50 Pedras, 3 Nervos de Urso, 500 Pedaços de Metal, 30 Minérios de Elunium e 100 Ouro.^000000";
		mes "Temos tudo isso, exceto as 50 Pedras!";
		next;
		switch(select("Reparar.:Deixar assim.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Ferreiro Guillaume]";
				mes "Você trouxe pedras suficientes! Vamos começar.";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combine pedras e ouro na porcentagem adequada.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combine Pedras e Ouro na porcentagem adequada e dê forma à Barricada.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combine Pedras e Ouro na porcentagem adequada e molde a Barricada, em seguida, adicione Elunium Bruto para torná-la mais forte.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combine Pedras e Ouro na porcentagem adequada e molde a Barricada, em seguida, adicione Elunium Bruto para torná-la mais forte. Agora decore com fragmentos de metal.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combine Pedras e Ouro na porcentagem adequada e molde a Barricada, em seguida, adicione Elunium Bruto para torná-la mais forte. Decore com fragmentos de metal e pedras de trança com tendão de urso!";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Ferreiro Guillaume]";
				mes "Uau! Está feito.";
				mes "Estamos aliviados.";
				if (mobcount("bat_a02","Barricada#bat_a02_a::OnMyMobDead") < 17) {
					delitem 7049,50; //Pedra
					donpcevent "Barricada#bat_a02_a::OnEnable";
				}
				close2;
				disablenpc "Ferreiro Guillaume#a02";
				end;
			}
			else {
				mes "[Ferreiro Guillaume]";
				mes "Você não tem pedras suficientes!";
				next;
				mes "[Ferreiro Guillaume]";
				mes "^3131FFPrecisamos de 50 pedras.^000000";
				mes "Estamos ocupados, por favor, se apresse.";
				close;
			}
		case 2:
			mes "[Ferreiro Guillaume]";
			mes "Há inimigos chegando! Vamos evacuar daqui!";
			close;
		}
	}
	else {
		mes "[Ferreiro Guillaume]";
		mes "Lá vem o inimigo!";
		close;
	}

OnInit:
	disablenpc "Ferreiro Guillaume#a02";
	end;
}

bat_a02,170,121,5	script	Ferreiro Croix#bat_a02	851,{
	if (getcharid(4) == $@TierraBG2_id2) {
		mes "[Ferreiro Croix]";
		mes "Estamos com urgência! A Barricada foi destruída!";
		mes "Nós podemos reparar a Barricada com ^3131FF50 Pedras, 3 Nervos de Urso, 500 Pedaços de Metal, 30 Minérios de Elunium e 100 Ouro.^000000";
		mes "E nós temos tudo isso, exceto as 50 Pedras!";
		next;
		switch(select("Reparar.:Deixar assim.")) {
		case 1:
			if (countitem(7049) > 49) {
				mes "[Ferreiro Croix]";
				mes "Você trouxe pedras suficientes! Vamos começar.";
				next;
				mes "..";
				next;
				mes "....";
				next;
				mes "......";
				next;
				mes "........";
				next;
				mes "..........";
				next;
				mes "............";
				next;
				mes "..............";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combine pedras e ouro na porcentagem adequada.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combine Pedras e Ouro na porcentagem adequada e dê forma à Barricada.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combine Pedras e Ouro na porcentagem adequada e molde a Barricada, em seguida, adicione Elunium Bruto para torná-la mais forte.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combine Pedras e Ouro na porcentagem adequada e molde a Barricada, em seguida, adicione Elunium Bruto para torná-la mais forte. Agora decore com fragmentos de metal.";
				next;
				mes "[Ferreiro Guillaume]";
				mes "Combine Pedras e Ouro na porcentagem adequada e molde a Barricada, em seguida, adicione Elunium Bruto para torná-la mais forte. Decore com fragmentos de metal e pedras de trança com tendão de urso!";
				next;
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				mes "..............";
				next;
				specialeffect EF_REPAIRWEAPON;
				mes "[Ferreiro Croix]";
				mes "Uau! Está feito.";
				mes "Estamos aliviados.";
				if (mobcount("bat_a02","Barricada#bat_a02_b::OnMyMobDead") < 17) {
					delitem 7049,50; //Pedra
					donpcevent "Barricada#bat_a02_b::OnEnable";
				}
				close2;
				disablenpc "Ferreiro Croix#bat_a02";
				end;
			}
			else {
				mes "[Ferreiro Croix]";
				mes "Você não tem pedras suficientes!";
				next;
				mes "[Ferreiro Croix]";
				mes "^3131FFPrecisamos de 50 Stone.^000000";
				mes "Estamos ocupados, por favor, se apresse.";
				close;
			}
		case 2:
			mes "[Ferreiro Croix]";
			mes "Há inimigos chegando! Vamos evacuar daqui!";
			close;
		}
	}
	else {
		mes "[Ferreiro Croix]";
		mes "Lá, o inimigo está chegando!";
		close;
	}

OnInit:
	disablenpc "Ferreiro Croix#bat_a02";
	end;
}

bat_a02,53,377,3	script	Terapeuta#a02_a	95,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta]";
	mes "Agora feche o seus olhos";
	mes "e respire bem fundo.";
	mes "Você pode se libertar da dor agora.";
	close;
	end;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Terapeuta#a02_a";
	end;

OnStop:
	// disablenpc "bat_a02_rp1_a_warp";
	disablenpc "Terapeuta#a02_a";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	// enablenpc "bat_a02_rp1_a_warp";
	areapercentheal "bat_a02",41,365,61,385,100,100;
	areawarp "bat_a02",41,365,61,385,"bat_a02",352,342;
	end;

// OnTimer26000:
	// disablenpc "bat_a02_rp1_a_warp";
	// end;

OnTimer26500:
	donpcevent "Terapeuta#a02_a::OnEnable";
	end;
}

/*
bat_a02,51,375,0	script	bat_a02_rp1_a_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a02_rp1_a_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_a02",352,342;
	end;
}
*/

bat_a02,45,19,3	script	Terapeuta#a02_b	95,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta]";
	mes "Agora feche o seus olhos";
	mes "e respire bem fundo.";
	mes "Você pode se libertar da dor agora.";
	close;
	end;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	enablenpc "Terapeuta#a02_b";
	end;

OnStop:
	// disablenpc "bat_a02_rp1_b_warp";
	disablenpc "Terapeuta#a02_b";
	stopnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	// enablenpc "bat_a02_rp1_b_warp";
	areapercentheal "bat_a02",33,7,53,27,100,100;
	areawarp "bat_a02",33,7,53,27,"bat_a02",353,52;
	end;

// OnTimer26000:
	// disablenpc "bat_a02_rp1_b_warp";
	// end;

OnTimer26500:
	donpcevent "Terapeuta#a02_b::OnEnable";
	end;
}

/*
bat_a02,43,17,0	script	bat_a02_rp1_b_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a02_rp1_b_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_a02",353,52;
	end;
}
*/

bat_a02,60,216,3	script	Fantasma#bat_a02_n	950,{
	specialeffect2 EF_HEAL;
	mes "[Fantasma]";
	mes "Boo...Boo...";
	close;

OnInit:
	initnpctimer;
	end;

OnEnable:
	stopnpctimer;
	initnpctimer;
	end;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	// enablenpc "bat_a02_rp1_n_warp";
	areapercentheal "bat_a02",45,203,65,223,100,100;
	areawarp "bat_a02",45,203,65,223,"bat_a02",301,209;
	end;

// OnTimer26000:
	// disablenpc "bat_a02_rp1_n_warp";
	// end;

OnTimer26500:
	donpcevent "Fantasma#bat_a02_n::OnEnable";
	end;
}

/*
bat_a02,55,213,0	script	bat_a02_rp1_n_warp	-1,10,10,{
OnInit:
	disablenpc "bat_a02_rp1_n_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_a02",301,209;
	end;
}
*/

bat_a02,194,267,0	script	barri_warp_up#bat_a02_a	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id1)
		warp "bat_a02",194,261;
	end;
}

bat_a02,194,265,0	script	barri_warp_down#bat_a02a	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id1) 
		warp "bat_a02",194,270;
	end;
}

bat_a02,177,130,0	script	barri_warp_up#bat_a02_b	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id2)
		warp "bat_a02",178,125;
	end;
}

bat_a02,177,128,0	script	barri_warp_down#bat_a02b	-1,7,0,{
OnTouch:
	if (getcharid(4) == $@TierraBG2_id2)
		warp "bat_a02",178,134;
	end;
}

bat_a02,352,342,0	script	A_CODE#bat_a02	-1,5,5,{
OnTouch:
	set .@chk_bfquest,checkquest(2069);
	if ((.@chk_bfquest == 0) || (.@chk_bfquest == 1))
		setquest 2069;
	end;
}

bat_a02,353,52,0	script	B_CODE#bat_a02	-1,5,5,{
OnTouch:
	set .@chk_bfquest,checkquest(2069);
	if ((.@chk_bfquest == 0) || (.@chk_bfquest == 1))
		setquest 2069;
	end;
}

bat_a02,169,227,0	warp	underladd#bat_a02_1	1,1,bat_a02,178,228
bat_a02,208,164,0	warp	underladd#bat_a02_2	1,1,bat_a02,200,171

bat_a02,171,309,3	script	Guillaume Camp#flag11	973,{ end; }
bat_a02,149,310,3	script	Guillaume Camp#flag12	973,{ end; }
bat_a02,119,336,3	script	Guillaume Camp#flag13	973,{ end; }
bat_a02,118,357,3	script	Guillaume Camp#flag14	973,{ end; }
bat_a02,150,380,3	script	Guillaume Camp#flag15	973,{ end; }
bat_a02,173,380,3	script	Guillaume Camp#flag16	973,{ end; }
bat_a02,210,344,3	script	Guillaume Camp#flag17	973,{ end; }
bat_a02,350,325,3	script	Guillaume Camp#flag18	973,{ end; }
bat_a02,358,325,3	script	Guillaume Camp#flag19	973,{ end; }

bat_a02,138,12,3	script	Croix Camp#flag11	974,{ end; }
bat_a02,108,36,3	script	Croix Camp#flag12	974,{ end; }
bat_a02,108,63,3	script	Croix Camp#flag13	974,{ end; }
bat_a02,136,87,3	script	Croix Camp#flag14	974,{ end; }
bat_a02,167,86,3	script	Croix Camp#flag15	974,{ end; }
bat_a02,199,49,3	script	Croix Camp#flag16	974,{ end; }
bat_a02,168,16,3	script	Croix Camp#flag17	974,{ end; }
bat_a02,357,74,3	script	Croix Camp#flag18	974,{ end; }
bat_a02,348,74,3	script	Croix Camp#flag19	974,{ end; }

bat_a02,53,377,3	script	Guillaume Vintenar#a02_a	419,{
	if (getcharid(4) == $@TierraBG2_id1) {
		if ($@TierraBG2_Victory == 1)
			callfunc "F_BG_Badge",1,"Guillaume","Tierra";
		else
			callfunc "F_BG_Badge",0,"Guillaume","Tierra";
	}
	else {
		if ($@TierraBG2_Victory == 2)
			callfunc "F_BG_Badge",0,"Guillaume","Tierra";
		else
			callfunc "F_BG_Badge",1,"Guillaume","Tierra";
	}
	bg_leave;
	if (!getbattleflag("feature.bgqueue"))
		warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Guillaume Vintenar#a02_a";
	end;
}

bat_a02,45,19,3	script	Croix Vintenar#a02_b	415,{
	if (getcharid(4) == $@TierraBG2_id2) {
		if ($@TierraBG2_Victory == 2)
			callfunc "F_BG_Badge",1,"Croix","Tierra";
		else
			callfunc "F_BG_Badge",0,"Croix","Tierra";
	}
	else {
		if ($@TierraBG2_Victory == 1)
			callfunc "F_BG_Badge",0,"Croix","Tierra";
		else
			callfunc "F_BG_Badge",1,"Croix","Tierra";
	}
	bg_leave;
	if (!getbattleflag("feature.bgqueue"))
		warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Croix Vintenar#a02_b";
	end;
}

bat_a02,1,5,3	script	countdown#bat_a02	844,{
OnEnable:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer7000:
	mapannounce "bat_a02", "Guillaume Vintenar Axl Rose: Vamos atacar para incendiar o Depósito de comida do Croix!",bc_map,"0xFF9900";
	end;

OnTimer8000:
	mapannounce "bat_a02", "Croix Vintenar Swandery: Mestre de Valhalla! Sejamos dotados de fé e coragem infalíveis!",bc_map,"0xFF99CC";
	end;

OnTimer1800000:
	mapannounce "bat_a02", "Marollo VII: Guillaume Marollo, Croix Marollo! Seguidores de Marollo!",bc_map,"0x99CC00";
	end;

OnTimer1803000:
	mapannounce "bat_a02", "Marollo VII: Ambos os campos são competitivos, então nenhum acampamento seria destruído facilmente. Isso significa que os reinos Marollo nunca serão derrotados!",bc_map,"0x99CC00";
	end;

OnTimer1808000:
	mapannounce "bat_a02", "Marollo VII: Acho melhor encerrarmos a batalha e chamar de empate.",bc_map,"0x99CC00";
	end;

OnTimer1822000:
	mapannounce "bat_a02", "Marollo VII: Mantenha sua realeza e fé por um momento e vamos encerrar a batalha de Tierra Gorge.",bc_map,"0x99CC00";
	end;

OnTimer1825000:
	mapannounce "bat_a02", "Axl Rose, Swandery: Sim, senhor.",bc_map,"0x99CC00";
	end;

OnTimer1830000:
	mapwarp "bat_a02","bat_room",154,150;
	donpcevent "countdown#bat_a02::OnStop";
	end;
}

/*
bat_a02,351,75,3	script	Soldado Croix#bat_a02_guide	934,{
	mes "Loading...";
	close;
}

bat_a02,356,326,3	script	Soldado Guillaume#bat_a02_guide	934,{
	mes "Loading...";
	close;
}
*/

bat_a02,1,1,3	script	Release all#a02	81,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "Cancelado.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "Eu posso te ajudar?";
		next;
		switch(select("Libertar todos.:Cancelar.")) {
		case 1:
			mes "Bye.";
			close2;
			mapwarp "bat_a02","bat_room",154,150;
			end;
		case 2:
			mes "Cancelado.";
			close;
		}
	}
}

// BG Queue makes these scripts useless
-	script	BGQueueInit#tierra02	-1,{
	end;

OnInit:
	if (getbattleflag("feature.bgqueue")) {
		unloadnpc "Tenente Rundel";
		unloadnpc "Tenente Guerrit";
		unloadnpc "#bat_a02_quest_a";
		unloadnpc "#bat_a02_quest_b";
		unloadnpc "back_bgrooma02a";
		unloadnpc "back_bgrooma02b";
		unloadnpc "A_CODE#bat_a02";
		unloadnpc "B_CODE#bat_a02";
	}
	end;
}
