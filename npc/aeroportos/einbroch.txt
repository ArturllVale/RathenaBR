//===== rAthena Script ======================================= 
//= Einbroch Equipe do Aeroporto
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Einbroch Equipe do Aeroporto
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

airport,143,43,5	script	Equipe do Aeroporto#airport1a::airport1	90,{
	mes "[Equipe do Aeroporto]";
	mes "Bem Vind"+(Sex?"o":"a")+" ao Aeroporto.";
	mes "Como eu posso ajudá-lo?";
	next;
	if (select("Embarcar no dirigível:Cancelar") == 1) {
		mes "[Equipe do Aeroporto]";
		mes "A taxa de embarque do Aeroplano";
		mes "é de 1,200 zeny, mas se você tiver";
		mes "um Bilhete para o Aeroplano,"; 
		mes "a taxa não será cobrada.";
		mes "Você quer embarcar no Aeroplano?";
		next;
		if (select("Sim:Nâo") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Bilhete para Aeroplano
				warp "airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				end;
			}
			mes "[Equipe do Aeroporto]";
			mes "Você não tem Zeny suficiente.";
			close;
		}
	}
	mes "[Equipe do Aeroporto]";
		mes "Obrigado e tenha";
		mes "um bom dia.";
	close;
}

airport,158,43,5	duplicate(airport1)	Equipe do Aeroporto#airport1b	90
airport,126,43,5	duplicate(airport1)	Equipe do Aeroporto#airport1c	90

airport,143,49,3	script	Arrival Staff#airport2a::airport2	90,{
	mes "[Arrival Staff]";
	mes "Welcome to Einbroch Airport.";
	mes "If you are arriving from your";
	mes "flight, let me guide you to the";
	mes "main terminal. Otherwise, please board the Airship to depart to";
	mes "Juno, Lighthalzen and Hugel.";
	next;
	if (select("Exit to main terminal.:Cancel.") == 1) {
		mes "[Arrival Staff]";
		mes "Once you're in the main terminal, you will need to pay the fee again";
		mes "to board an Airship. You should";
		mes "only exit if the city of Einbroch";
		mes "is your intended destination.";
		mes "Proceed to the main terminal?";
		next;
		if (select("Yes:No") == 1) {
			warp "airport",142,40;
			end;
		}
	}
	mes "[Arrival Staff]";
	mes "Alright, thank you";
	mes "for your patronage";
	mes "and I hope you have";
	mes "a pleasant flight~";
	close;
}

airport,126,51,3	duplicate(airport2)	Arrival Staff#airport2b	90
airport,158,50,3	duplicate(airport2)	Arrival Staff#airport2c	90

einbroch,94,267,3	script	Airship Staff#ein01	91,{
	mes "[Airship Staff]";
	mes "Welcome to the";
	mes "Einbroch Airport.";
	mes "Please use this door to";
	mes "board the Airship which stops";
	mes "over Juno, Lighthalzen and";
	mes "Hugel in the Schwarzwald Republic.";
	next;
	mes "[Airship Staff]";
	mes "Otherwise, if Einbroch is";
	mes "your intended destination,";
	mes "please head down the stairs";
	mes "and ask the Arrival Staff to lead";
	mes "you to the main terminal. Thank";
	mes "you, and enjoy your travels.";
	close;
}
