// ===== Script rAthena ===========================================
// = NPC de remoção de cartão
// ===== Por: =============================================== =========
// = TyrNemesis ^
//===== Traduzido por: ================================================== 
//= Guru
// ===== Versão Atual: ===========================================
// = 1.2a
// ===== Compatível com: =========================================
// = Projeto rAthena
// ===== Descrição: ===============================================
// = Remove cartas de itens equipados.
// ===== Comentários adicionais: ===================================
// = 1.0 Primeira versão. [TyrNemesis ^]
// = 1.2 Menu getequipname otimizado e fixo. [Kisuka]
// = 1.2a Adicionado comando 'disable_items'. [Euphy]
//============================================================

prt_in,28,73,4	script	Velha Sábia#eAcustom	78,{

	set .zenycost,200000;    // custo base dos serviços de remoção de cartão (em Zeny)
	set .percardcost,25000;  //custo por cartão dos serviços de remoção de cartão (em Zeny)
	set .faildestroy,1;      // o removedor de cartão deve ter uma chance de falha que destrói itens? (1 = sim, 0 = não)

	disable_items;
	mes "[Velha Sábia]";
	mes "Bom dia, "+(Sex ? "meu" : "minha")+" jovem. Tenho o poder de remover cartas que você combinou em seu equipamento. Essa ideia te agrada?";
	next;
	switch(select("^CC6600Sim^000000:^003300Quanto você cobra?^000000:^993300Não, obrigad"+(Sex ? "o" : "a")+"^000000.")) {
	case 1:
		mes "[Velha Sábia]";
		mes "Muito bem. Qual item devo examinar para você?";
		next;

		setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
		for( set .@i,1; .@i <= 10; set .@i,.@i+1 ) {
			if( getequipisequiped(.@indices[.@i]) )
				set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			set .@menu$, .@menu$ + ":";
		}
		set .@part, .@indices[ select(.@menu$) ];
		if(!getequipisequiped(.@part)) {
			mes "[Velha Sábia]";
			mes "Jovem... Equipe algo para que eu possa trabalhar. Não posso fazer nada por você assim.";
			close;
		}
		if(getequipcardcnt(.@part) == 0) {
			mes "[Velha Sábia]";
			mes "Jovem... Não há cartas equipadas neste ítem. Não posso trabalhar assim!";
			close;
		}
		set .@cardcount,getequipcardcnt(.@part);
		
		if (!checkweight(1202,(.@cardcount+1))) { 
			mes "^3355FFAguarde um momento!";
			mes "Eu não posso oferecer meus serviços a você pois está com excesso de peso ou itens no iventário.";
			mes "Utilize seu armazém e volte até mim~";
			close;
		}
		mes "[Velha Sábia]";
		mes "Este item tem " + .@cardcount + " cartas compostos sobre ele. Para realizar minha magia, vou precisar " + (.zenycost+(.@cardcount * .percardcost)) + " zeny, uma ^0000FFFragmento Estelar^000000, e uma ^0000FFGema Amarela^000000.";
		next;
		if(select("Muito bem. Faça.:Deixa pra lá.") == 2) {
			mes "[Velha Sábia]";
			mes "Muito bem, agradeço por utilizar os meus serviços!";
			close;
		}
		if((zeny < (.zenycost+(.@cardcount * .percardcost))) || (countitem(1000) < 1) || (countitem(715) < 1)) {
			mes "[Velha Sábia]";
			mes "Você não tem tudo que é necessário para a minha mágica, garot"+(sex > 0 ? "o" : "a")+". Volte quando conseguí-los.";
			close;
		}
		mes "[Velha Sábia]";
		mes "Antes de começar, devo avisá-lo - posso falhar. Se o fizer, posso destruir as cartas, o item ou ambos. Eu não dou reembolso. Dito isso, o que é mais importante para você: as cartas ou o item?";
		next;
		switch(select("Mudei de idéia, Tchau:O item.:As cartas.")) {
		case 1:
			mes "[Velha Sábia]";
			mes "Muito bem. Volte quando precisar dos meus serviços.";
			close;
		case 2:
			set .@failtype,1;
			break;
		case 3:
			set .@failtype,2;
		}
		mes "[Velha Sábia]";
		mes "Muito bem. Eu devo começar.";
		set Zeny, Zeny - (.zenycost+(.@cardcount * .percardcost));
		delitem 1000,1; //Fragmento Estelar
		delitem 715,1; //Gema Amarela
		
	// Substitua as constantes nas próximas 3 linhas por valores de chance de falha definidos em refine_db.txt
	// Primeiro valor = chance de falha total (item e cartas destruídas)
	// Segundo valor = Chance de falha parcial (um ou outro é destruído, o jogador decide qual é seguro)
	// Terceiro valor = chance de falha inofensiva (tudo o que se perde é o seu investimento)

		set .@failchance,rand(100);
		if (.faildestroy==1) {
			if(.@failchance < 2) {
				next;
				failedremovecards .@part,0;
				mes "[Velha Sábia]";
				mes "O processo foi um fracasso total. Receio que o item e os cartas tenham sido destruídos.";
				close;
			}

			if(.@failchance < 8) {
				if (.@failtype == 1) {
					next;
					failedremovecards .@part,1;
					mes "[Velha Sábia]";
					mes "Embora eu tenha conseguido remover os cartas do item, eles foram destruídos no processo. O item, entretanto, está bom.";
					close;
				}

				if (.@failtype == 2) {
					next;
					failedremovecards .@part,2;
					mes "[Velha Sábia]";
					mes "Muito infeliz. Consegui remover as cartas, mas o próprio item foi destruído no processo.";
					close;
				}
			}
		}

		if(.@failchance < 10) {
			next;
			failedremovecards .@part,3;
			mes "[Velha Sábia]";
			mes "Não consegui remover as cartas. Felizmente, no entanto, tanto o item quanto as cartas ainda estão corretos.";
			close;
		}
		next;
		successremovecards .@part;
		mes "[Velha Sábia]";
		mes "O processo foi um sucesso. Aqui estão suas cartas e seu item. Despedida.";
		close;
	case 2:
		mes "[Velha Sábia]";
		mes "Eu cobro uma taxa fixa de "+callfunc("F_InsertComma",.zenycost)+" zeny, mais "+callfunc("F_InsertComma",.percardcost)+" zeny para cada cartão que removo do item. Além disso, preciso de um Fragmento Estelar e uma Gema Amarela para fazer minha mágica.";
		close;
	case 3:
		mes "[Velha Sábia]";
		mes "Muito bem. Volte quando precisar dos meus serviços.";
		close;
	}
}
