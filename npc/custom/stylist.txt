// ===== Script rAthena ===========================================
// = Estilista
// ===== Por: =============================================== =========
// = Euphy
// ===== Traduzido Por: =============================================== =========
// = Guru
// ===== Versão Atual: ===========================================
// = 1,1
// ===== Compatível com: =========================================
// = Projeto rAthena
// ===== Descrição: ===============================================
// = Altera o estilo do cabelo, a cor do cabelo e a cor da roupa.
// ===== Comentários adicionais: ===================================
// = 1.0 Script inicial.
// = 1.1 Trocado para 'getbattleflag', créditos para Saithis. [Euphy]
//============================================================

prontera,170,180,1	script	Estilista#custom_stylist	122,{
	setarray .@Styles[1],
		getbattleflag("max_cloth_color"),
		getbattleflag("max_hair_style"),
		getbattleflag("max_hair_color");
	setarray .@Look[1],
		LOOK_CLOTHES_COLOR,
		LOOK_HAIR,
		LOOK_HAIR_COLOR;
	set .@s, select("[^000033—^000000]^3300ccCor da Roupa^000000:[^9900ff—^000000]^cc33ccEstilo do Cabelo^000000:[^993366—^000000]^660033Cor do Cabelo^000000");
	set .@Revert, getlook(.@Look[.@s]);
	set .@Style,1;
	while(1) {
		setlook .@Look[.@s], .@Style;
		message strcharinfo(0),"Estilo de numero: #"+.@Style+".";
		set .@menu$, "[^66CC00»^000000]^663300Próximo^000000 (^66CC00"+((.@Style!=.@Styles[.@s])?.@Style+1:1)+"^000000):[^990000«^000000]^663300Anterior^000000 (^990000"+((.@Style!=1)?.@Style-1:.@Styles[.@s])+"^000000):[^3399FF¤^000000]^663300Digitar^000000:[^66CCCCø^000000]^663300Resetar^000000 (^66CCCC"+.@Revert+"^000000)";
		switch(select(.@menu$)) {
		case 1:
			set .@Style, ((.@Style != .@Styles[.@s]) ? .@Style+1 : 1);
			break;
		case 2:
			set .@Style, ((.@Style != 1) ? .@Style-1 : .@Styles[.@s]);
			break;
		case 3:
			message strcharinfo(0),"Escolha um estilo entre 1 - "+.@Styles[.@s]+".";
			input .@Style,0,.@Styles[.@s];
			if (!.@Style)
				set .@Style, rand(1,.@Styles[.@s]);
			break;
		case 4:
			set .@Style, .@Revert;
			setlook .@Look[.@s], .@Revert;
			break;
		}
	}
}
