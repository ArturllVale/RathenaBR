// ===== Script rAthena ===========================================
// = MVP privado e sala da filial
// ===== Por: =============================================== =========
// = AnnieRuru
// ===== Traduzido Por: =============================================== =========
// = Guru
// ===== Versão Atual: ===========================================
// = 1.0
// ===== Compatível com: =========================================
// = Projeto rAthena
// ===== Descrição: ===============================================
// = Permite aos jogadores alugar uma sala MVP para uso pessoal,
// = ou para um partido ou guilda.
// ===== Comentários adicionais: ===================================
// = 1.0 Primeira versão, editada. [Euphy]
//============================================================

prontera,148,174,5	script	Sala MVP privada	100,{
	mes "[Sala MVP privada]";
	mes "Selecione umaa Sala MVP privada.";
	if ( getvariableofnpc( .rentcost, "MVP Summoner" ) )
		mes "O custo de alugar uma sala "+ getvariableofnpc( .timeout, "MVP Summoner" ) +" é de "+ callfunc("F_InsertComma", getvariableofnpc( .rentcost, "MVP Summoner" ) ) +" zeny por minuto";
	else
		mes "Você só pode usar a aala por apenas "+ getvariableofnpc( .timeout, "MVP Summoner" ) +" minutos.";
	mes " ";
	for ( .@i = 1; .@i <= 8; .@i++ )
		if ( getvariableofnpc( .renttime[.@i], "MVP Summoner" ) )
			mes "Sala #"+ .@i +" = "+ .color$[ .type[.@i] ] + .whoinuse$[.@i] +"^000000";
	next;
	.@room = select(
		"MVP Room 1 ["+ getmapusers("06guild_01") +"]",
		"MVP Room 2 ["+ getmapusers("06guild_02") +"]",
		"MVP Room 3 ["+ getmapusers("06guild_03") +"]",
		"MVP Room 4 ["+ getmapusers("06guild_04") +"]",
		"MVP Room 5 ["+ getmapusers("06guild_05") +"]",
		"MVP Room 6 ["+ getmapusers("06guild_06") +"]",
		"MVP Room 7 ["+ getmapusers("06guild_07") +"]",
		"MVP Room 8 ["+ getmapusers("06guild_08") +"]");
	if ( getvariableofnpc( .renttime[.@room], "MVP Summoner" ) ) {
		if ( .inuseid[.@room] == getcharid( .type[.@room] ) ) {
			warp "06guild_0"+ .@room, 0,0;
			close;
		} else {
			mes "[Sala MVP privada]";
			mes "Esta sala está reservado para ";
			mes .color$[ .type[.@room] ] + .whoinuse$[.@room] +"^000000.";
			mes "Selecione outro.";
			close;
		}
	}
	mes "[Sala MVP privada]";
	mes "Reserve esta sala para...";
	next;
	set .@type, select( "^333366Para meu Grupo^000000", "^cc6600Para minha Guild^000000", "^990066Para uso Pessoal^000000" );
	if ( !getcharid(.@type) ) {
		mes "[Sala MVP privada]";
		mes "Você não possui um "+( ( .@type == 1 )? "Grupo" : "Guild" )+".";
		close;
	}
	else if ( Zeny < getvariableofnpc( .rentcost, "MVP Summoner" ) ) {
		mes "Você não tem zeny suficiente para alugar uma sala.";
		close;
	}
	else if ( getvariableofnpc( .renttime[.@room], "MVP Summoner" ) ) {
		mes "[Sala MVP privada]";
		mes "Desculpe, outra pessoa já registrou esta sala mais rápido do que você.";
		close;
	}
	for ( .@i = 1; .@i <= 8; .@i++ ) {
		if ( ( getvariableofnpc( .renttime[.@i], "MVP Summoner" ) ) && .@type == .type[.@i] && getcharid(.@type) == .inuseid[.@i] ) {
			mes "[Sala MVP privada]";
			mes "Você já alugou a sala#"+ .@i +". Em vez disso, use aquela sala.";
			close;
		}
	}
	set .type[.@room], .@type;
	set .inuseid[.@room], getcharid(.@type);
	set .whoinuse$[.@room], strcharinfo( ( .@type == 3 )? 0 : .@type );
	Zeny -= getvariableofnpc( .rentcost, "MVP Summoner" );
	warp "06guild_0"+ .@room, 0,0;
	killmonsterall "06guild_0"+ .@room;
	donpcevent "MVP Summoner#"+ .@room +"::OnEnterMap";
	close;
OnInit:
	.color$[1] =  "^EE8800"; // cor do grupo
	.color$[2] =  "^70CC11"; // cor da guild
	.color$[3] =  "^0000FF"; // cor particular
	end;
}

-	script	MVP Summoner	-1,{
	mes "[Sumonador MVP]";
	mes "Tempo à esquerda: " + callfunc( "Time2Str", .renttime[ atoi( strnpcinfo(2) ) ] + .timeout * 60 );
	mes "Oi, o que posso fazer por você?";
	next;
	switch ( select(.menu$) ) {
	case 1:
		mes "[Sumonador MVP]";
		if ( mobcount( "this", strnpcinfo(3)+"::OnMobDead" ) ) {
			mes "Eu não posso oferecer serviço de cura quando há monstros ao redor.";
			close;
		}
		sc_end SC_STONE;
		sc_end SC_SLOWDOWN;
		sc_end SC_FREEZE;
		sc_end SC_SLEEP;
		sc_end SC_CURSE;
		sc_end SC_SILENCE;
		sc_end SC_CONFUSION;
		sc_end SC_BLIND;
		sc_end SC_BLEEDING;
		sc_end SC_DECREASEAGI;
		sc_end SC_POISON;
		sc_end SC_HALLUCINATION;
		sc_end SC_STRIPWEAPON;
		sc_end SC_STRIPARMOR;
		sc_end SC_STRIPHELM;
		sc_end SC_STRIPSHIELD;
		sc_end SC_CHANGEUNDEAD;
		sc_end SC_ORCISH;
		sc_end SC_BERSERK;
		sc_end SC_SKE;
		sc_end SC_SWOO;
		sc_end SC_SKA;
		percentheal 100,100;
		specialeffect2 EF_HEAL;
		mes "Você está completamente curado.";
		close;
	case 2:
		if ( mobcount( "this", strnpcinfo(3)+"::OnMobDead" ) ) {
			mes "[Sumonador MVP]";
			mes "Eu não posso invocar outro MVP quando há monstros ao redor.";
			close;
		} else if ( .mvpcost ) {
			mes "[Sumonador MVP]";
			mes "O custo para invocar um MVP é "+ callfunc( "F_InsertComma", .mvpcost ) +" zeny.";
			next;
			set .@menu, select(.mvpid_menu$) -1;
			mes "[Sumonador MVP]";
			if ( Zeny < .mvpcost ) {
				mes "Você não tem zeny suficiente para invocar um MVP.";
				close;
			}
		} else {
			set .@menu, select(.mvpid_menu$) -1;
			mes "[Sumonador MVP]";
		}
		mes "Por favor, prepare-se.";
		close2;
		if ( Zeny < .mvpcost ) end;
		Zeny -= .mvpcost;
		monster "this", 0, 0, "--ja--", .mvpid[.@menu], 1, strnpcinfo(3)+"::OnMobDead";
		end;
	case 3:
		if ( mobcount( "this", strnpcinfo(3)+"::OnMobDead" ) ) {
			mes "[Sumonador MVP]";
			mes "Eu não posso chamar outro mini-chefe quando há monstros ao redor.";
			close;
		} else if ( .bosscost ) {
			mes "[Sumonador MVP]";
			mes "O custo para invocar um mini-chefe é "+ callfunc( "F_InsertComma", .bosscost ) +" zeny.";
			next;
			set .@menu, select(.bossid_menu$) -1;
			mes "[Sumonador MVP]";
			if ( Zeny < .bosscost ) {
				mes "Você não tem zeny suficiente para invocar um mini-chefe.";
				close;
			}
		} else {
			set .@menu, select(.bossid_menu$) -1;
			mes "[Sumonador MVP]";
		}
		mes "Por favor, prepare-se.";
		close2;
		if ( Zeny < .bosscost ) end;
		Zeny -= .bosscost;
		monster "this", 0, 0, "--ja--", .bossid[.@menu], 1, strnpcinfo(3)+"::OnMobDead";
		end;
	case 4:
		if ( mobcount( "this", strnpcinfo(3)+"::OnMobDead" ) > 0 ) {
			mes "[Sumonador MVP]";
			mes "Eu não posso oferecer este serviço quando há monstros ao redor.";
			close;
		}
		close2;
		callshop "MVP room#branch", 1;
		end;
	case 5:
		mes "[Sumonador MVP]";
		mes "Tem certeza que quer sair desta sala?";
		next;
		if ( select( "Sim:Não") == 1 )
			warp .respawnmap$, .respawnx, .respawny;
		close;
	case 6:
		mes "[Sumonador MVP]";
		if ( getmapusers( strcharinfo(3) ) > 1 ) {
			mes "Ainda há alguns jogadores nesta sala. Certifique-se de que você é o último membro nesta sala a usar essa opção.";
			close;
		}
		mes "Tem certeza de que deseja desistir desta sala?";
		if ( .rentcost )
			mes "Você precisará pagar novamente para entrar nesta sala.";
		next;
		if ( select( "Sim", "Não" ) == 2 ) close;
		awake strnpcinfo(0);
		end;
	}
	close;

OnMobDead:
	end;

OnEnterMap:
	.@id = atoi( strnpcinfo(2) );
	.renttime[.@id] = gettimetick(2);
	sleep .timeout * 60000;
	mapwarp strnpcinfo(4), .respawnmap$, .respawnx, .respawny;
	.renttime[.@id] = 0;
	killmonsterall strnpcinfo(4);
	end;

OnInit:
	if ( !getstrlen( strnpcinfo(2) ) ) {

//	Config ---------------------------------------------------------------------------------------

	// Tempo de aluguel do quarto, em minutos.
	// Quando o tempo acabar, todos os jogadores dentro da sala serão expulsos.
	// NÃO defina isso para zero!
	set .timeout, 60;

	set .rentcost, 100000;	// Custo Zeny para alugar um quarto (0 = grátis)
	set .mvpcost, 100000;	// Custo Zeny para invocar um MVP (0 = grátis)
	set .bosscost, 50000;	// Custo Zeny para invocar um Mini-boss (0 = grátis)

	//	Configuração de opções dentro da sala MVP
	set .@menu[1], 1;	// Ativar / desativar a opção Heal
	set .@menu[2], 1;	// Ligar / desligar a invocação MVP
	set .@menu[3], 1;	// Ligar / Desligar a Invocação de Mini Boss
	set .@menu[4], 0;	// Vender itens (filiais) On / Off (veja a loja abaixo, antes dos mapflags)

	// Ponto de respawn quando os jogadores saem da sala
	set .respawnmap$, "prontera";
	set .respawnx, 150;
	set .respawny, 174;

	setarray .mvpid[0],
		1511,//	Amon-Ra
		1647,// Algoz Eremes
		1785,//	Atroce
		1630,//	Lady Branca
		1039,//	Bafomé
		1874,//	Belzebu
		1272,//	Senhor das Trevas
		1719,//	Detardeurus
		1046,//	Doppelgangger
		1389,//	Dracula
		1112,//	Drake
		1115,//	Eddga
		1418,//	Serpente Suprema
		1871,//	Bispo Decadente
		1252,//	Hatii
		1768,//	Pesar Noturno
		1086,//	Besouro-Ladrão Dourado
		1885,//	Gopinich
		1649,// Suma-Sacerdotisa Margaretha
		1651,// Arquimaga Kathryne
		1832,//	Ifrit
		1492,//	Samurai Encarnado
		1734,//	Kiel D-01
		1251,//	Cavaleiro da Tempestade
		1779,//	Ktullanux
		1688,//	Lady Tanee
		1646,// Lorde Seyren
		1373,//	Senhor dos Mortos
		1147,//	Maya
		1059,//	Abelha-Rainha
		1150,//	Flor do Luar
		1087,//	Orc Herói
		1190,//	Senhor dos Orcs
		1038,//	Osíris
		1157,//	Faraó 
		1159,//	Freeoni 
		1623,//	RSX 0806
		1650,// Atiradora de Elite Cecil 
		1583,//	Tao Gunka
		1708,//	Thanatos
		1312,//	General Tartaruga
		1751,//	Valquíria Randgris
		1685,// Vesper
		1648,// Mestre-Ferreiro Howard
		1917,// Morroc Ferido
		1658;//	Egnigem Cenia

	setarray .bossid[0],
		1096,// Angeling
		1388,// Archangeling
		1795,// Cavaleiro Sanguinário
		1830,// Arqueiro Guardião
		1839,// Byorgue
		1307,// Gato de Nove Caudas
		1283,// Quimera 
		1302,// Ilusão das Trevas
		1198,// Sacerdote Maldito
		1582,// Deviling
		1091,// Libélula
		1093,// Eclipse 
		1205,// Executor 
		1783,// Galion
		1592,// Yakuza 
		1120,// Ghostring
		1259,// Grifo 
		1720,// Hydrolancer 
		1090,// Mastering
		1289,// Maya Macho 
		1262,// Dragão Mutante
		1203,// Mysteltainn
		1870,// Necromante 
		1295,// Barão Coruja
		1829,// Espadachim Guardião
		1204,// Tyrfing 
		1089,// Sapo-Cururu
		1092,// Lobo Errante
		1765;// Valquíria 

//	Fim Config ------------------------------------------------------------------------

		if ( !.timeout ) set .timeout, 60;
		.menu$ = ( .@menu[1] ? "Curar" : "" ) +":" ;
		.menu$ = .menu$ + ( .@menu[2] ? "Invocar MVP" : "" ) +":";
		.menu$ = .menu$ + ( .@menu[3] ? "Invocar Mini-boss" : "" ) +":";
		.menu$ = .menu$ + ( .@menu[4] ? "Comprar suprimentos" : "" ) +":";
		.menu$ = .menu$ + "- Deixa esta sala:- Desista desta sala";
		if ( .@menu[2] ) {
			.@size = getarraysize( .mvpid );
			for ( .@i = 0; .@i < .@size; .@i++ )
				.mvpid_menu$ = .mvpid_menu$ + getmonsterinfo( .mvpid[.@i], MOB_NAME ) +":";
		}
		if ( .@menu[3] ) {
			.@size = getarraysize( .bossid );
			for ( .@i = 0; .@i < .@size; .@i++ )
				.bossid_menu$ = .bossid_menu$ + getmonsterinfo( .bossid[.@i], MOB_NAME ) +":";
		}
	}
	else {
		mapannounce strnpcinfo(4), "Um administrador atualizou o servidor. Por favor, registre novamente este quarto.", bc_map;
		mapwarp strnpcinfo(4), .respawnmap$, .respawnx, .respawny;
	}
	end;
}

-	shop	MVP room#branch	-1,604:100000,12103:1000000

06guild_01,49,49,4	duplicate(MVP Summoner)	MVP Summoner#1	116
06guild_02,49,49,4	duplicate(MVP Summoner)	MVP Summoner#2	116
06guild_03,49,49,4	duplicate(MVP Summoner)	MVP Summoner#3	116
06guild_04,49,49,4	duplicate(MVP Summoner)	MVP Summoner#4	116
06guild_05,49,49,4	duplicate(MVP Summoner)	MVP Summoner#5	116
06guild_06,49,49,4	duplicate(MVP Summoner)	MVP Summoner#6	116
06guild_07,49,49,4	duplicate(MVP Summoner)	MVP Summoner#7	116
06guild_08,49,49,4	duplicate(MVP Summoner)	MVP Summoner#8	116

06guild_01	mapflag	nowarpto
06guild_02	mapflag	nowarpto
06guild_03	mapflag	nowarpto
06guild_04	mapflag	nowarpto
06guild_05	mapflag	nowarpto
06guild_06	mapflag	nowarpto
06guild_07	mapflag	nowarpto
06guild_08	mapflag	nowarpto
06guild_01	mapflag	nomemo
06guild_02	mapflag	nomemo
06guild_03	mapflag	nomemo
06guild_04	mapflag	nomemo
06guild_05	mapflag	nomemo
06guild_06	mapflag	nomemo
06guild_07	mapflag	nomemo
06guild_08	mapflag	nomemo
06guild_01	mapflag	noteleport
06guild_02	mapflag	noteleport
06guild_03	mapflag	noteleport
06guild_04	mapflag	noteleport
06guild_05	mapflag	noteleport
06guild_06	mapflag	noteleport
06guild_07	mapflag	noteleport
06guild_08	mapflag	noteleport
06guild_01	mapflag	nosave	SavePoint
06guild_02	mapflag	nosave	SavePoint
06guild_03	mapflag	nosave	SavePoint
06guild_04	mapflag	nosave	SavePoint
06guild_05	mapflag	nosave	SavePoint
06guild_06	mapflag	nosave	SavePoint
06guild_07	mapflag	nosave	SavePoint
06guild_08	mapflag	nosave	SavePoint
06guild_01	mapflag	nopenalty
06guild_02	mapflag	nopenalty
06guild_03	mapflag	nopenalty
06guild_04	mapflag	nopenalty
06guild_05	mapflag	nopenalty
06guild_06	mapflag	nopenalty
06guild_07	mapflag	nopenalty
06guild_08	mapflag	nopenalty
