// ===== Script rAthena ===========================================
// = O 2º Banco de Prontera (com receita diária de 0,01%!)
// ===== Por: =============================================== =========
// = Lúpus (1.0)
// ===== Traduzido Por: =============================================== =========
// = Guru
// ===== Versão Atual: ===========================================
// = 1.2a
// ===== Compatível com: =========================================
// = Projeto rAthena
// ===== Descrição: ===============================================
// = Um banco que tem uma% de juros
// ===== Comentários adicionais: ===================================
// Procure por esta fórmula e configure sua% de juros diária do banco
// # kafrabank/1000 = 0,1% de juros por dia
// # kafrabank/100 = 1% de juros por dia
// # kafrabank/10 = 10% de juros por dia
//
// 1.1 Adicionado log de operação do banco -> logmes "some info";
// 1.2 Defina a renda máxima para 100.000 z. Ajudaria a evitar
// zeny explora quando você altera DATA em seu servidor
// 1.2a Nomes duplicados incorretos corrigidos. (relatório de bug: 921) [Samuray22]
//============================================================

-	script	Funcionário do banco::bankg	833,{
	mes"[Maniss]";
	mes strcharinfo(0)+", bem vind"+(Sex?"o":"a")+" ao 2º Banco de Prontera!";

	set @kb_int,(gettime(DT_MONTH)*31)+gettime(DT_DAYOFMONTH); //número de hoje
	set @income,0;
	//calcular %
	if (#kafrabank<=0 || #kb_int>=@kb_int) goto L_NoIncomeToday;
	set @income,(#kafrabank/1000)*(@kb_int-#kb_int); //@income == % da soma
	//constante de renda máxima:
	if (@income>100000) set @income,100000;
L_NoIncomeToday:
	set #kb_int,@kb_int; // redefinir cronômetro de dias

	if(#kafrabank==0) mes "Podemos abrir uma conta para você.";
	if(@income>0) mes "Renda de hoje: ^135445" + callfunc("F_InsertComma",@income) + "^000000 zeny.";
	set #kafrabank,#kafrabank+@income;
	if(#kafrabank>0) mes "Sua conta: ^135445" + callfunc("F_InsertComma",#kafrabank) + "^000000 zeny.";
	mes "O que você gostaria?";
	next;
	if(#kafrabank==0) menu "-Abrir uma conta",-,"-Sair",B_EXIT2;
	if(#kafrabank>0) menu "-Depositar",-,"-Retirar",M_WITHDRAW,"-Sair",B_EXIT2;

	mes"[Maniss]";
	mes "Por favor, diga-me quanto zeny você gostaria de depositar.";
	next;
	if(input(@kafrabank) == 1) goto L_TOO_BIG_AMOUNT;

	if(@kafrabank<1000) goto L_LESS_1000;
	if(@kafrabank>zeny) goto L_NOT_ENOUGH;
	set Zeny, Zeny-@kafrabank;
	set #kafrabank,#kafrabank+@kafrabank;
	mes"[Maniss]";
	mes "Você fez um depósito de ^135445" + callfunc("F_InsertComma",@kafrabank) + "z^000000.";
	//registramos essas operações zeny no banco de dados de log
	logmes "Bank %: " + @income +"z, Deposit: "+ @kafrabank +"z, Final: "+ #kafrabank +"z";
	goto B_EXIT;

M_WITHDRAW:
	if(#kafrabank==0) goto L_ZERO_ACCOUNT;
	mes"[Maniss]";
	mes "Sua conta: ^135445" + callfunc("F_InsertComma",#kafrabank) + "^000000 zeny.";
	mes "Quanto zeny você gostaria de sacar?";
	next;
	if(input(@kafrabank) == 1) goto L_TOO_BIG_AMOUNT;

	if(@kafrabank<1) goto B_EXIT2;
	if(@kafrabank>#kafrabank) goto L_NOT_ENOUGH;
	set #kafrabank,#kafrabank-@kafrabank;
	set Zeny, Zeny+@kafrabank;
	mes"[Maniss]";
	mes "Aqui está o seus ^135445" + callfunc("F_InsertComma",@kafrabank) + "z^000000, volte sempre.";
	//registramos essas operações zeny no banco de dados de log
	logmes "Bank %: " + @income +"z, Withdraw: "+ @kafrabank +"z, Final: "+ #kafrabank +"z";
	goto B_EXIT;

L_NOT_ENOUGH:
	mes"[Maniss]";
	mes "Você não tem zeny suficiente para esta operação.";
	next;
	goto B_EXIT2;

L_ZERO_ACCOUNT:
	mes"[Maniss]";
	mes "Você não tem nenhum zeny em sua conta!";
	next;
	goto B_EXIT2;

L_TOO_BIG_AMOUNT:
	mes"[Maniss]";
	mes "Desculpe. O depósito máximo que você pode fazer por vez é 10.000.000 zeny.";
	next;
	goto B_EXIT2;

L_LESS_1000:
	mes"[Maniss]";
	mes "Desculpe, o valor mínimo de zeny que você pode depositar é 1.000 zeny.";
	next;
	goto B_EXIT2;

B_EXIT:
	mes "Muito bem... Volte logo!";
	next;

B_EXIT2:
	mes"[Maniss]";
	mes "Obrigado por usar nosso serviço bancário. Esperamos te ver novamente em breve.";
	close;
}

prontera,131,190,1	duplicate(bankg)	Bank Clerk#1-1	833
geffen,125,73,3	duplicate(bankg)	Bank Clerk#2-1	833
izlude,145,107,1	duplicate(bankg)	Bank Clerk#3-1	833
morocc,147,84,7	duplicate(bankg)	Bank Clerk#4-1	833
