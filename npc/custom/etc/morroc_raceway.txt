// ===== Script rAthena ===========================================
// = Morocc Raceway
// ===== Por: =============================================== =========
// = acky
// ===== Traduzido Por: =============================================== =========
// = Guru
// ===== Versão Atual: ===========================================
// = 1,3
// ===== Compatível com: =========================================
// = Projeto rAthena
// ===== Descrição: ===============================================
// = Permite que os jogadores corram em Morocc (pvp_y_1-5)
// ===== Comentários adicionais: ===================================
// = Se houver mais de 3 jogadores, pelo menos 3 pessoas
// = deve terminar antes que uma nova corrida possa ser iniciada.
// =
// = Se houver menos de 3 jogadores, pelo menos 1 pessoa
// = deve terminar antes que uma nova corrida possa ser iniciada.
// =
// = Variáveis ??globais permanentes removidas
// = 1.2 Duplicados removidos [Silencioso]
// = 1.3 Números de efeito substituídos por constantes. [Samuray22]
//============================================================

// Leva você para o modo de corrida
morocc,166,105,6	script	Garota da Corrida#01	116,{
	mes "[Garota da Corrida]";
	mes "Você gostaria de visitar^0000FFMorocc Raceway^000000?";
	next;
	menu "Yes",L_Warp,"No",-;
	mes "[Garota da Corrida]";
	mes "Tudo bem, fale comigo novamente quando quiser ir.";
	close;
L_Warp:
	warp "pvp_y_1-5",165,256;
	close;
}

//Tira você da pista
pvp_y_1-5,169,265,5	script	Garota da Corrida#02	116,{
	mes "[Garota da Corrida]";
	mes "Bem-vind"+(Sex?"o":"a")+" ao Morroc Raceway!";
	next;
	menu "Information",-,"Leave",L_Warp,"Cancel",L_Cancel;
	mes "[Garota da Corrida]";
	mes "Alguém deve clicar no NPC Starter para iniciar a corrida.";
	next;
	mes "[Garota da Corrida]";
	mes "Assim que a corrida começar, rode Morocc no sentido anti-horário.";
	next;
	mes "[Garota da Corrida]";
	mes "Você deve alcançar todos os pontos de verificação - Sem trapaça!";
	close;
L_Warp:
	warp "morocc",165,101;
L_Cancel:
	mes "[Garota da Corrida]";
	mes "Volte logo!";
	close;
}

//Faz a contagem regressiva e começa a corrida
pvp_y_1-5,145,269,5	script	Starter	733,{
	if ($@race != 0) goto L_Started;
	if ($@counting != 0) goto L_Started;
	if ($@racecount == 1) goto L_Started;
L_Menu:
	mes "[Iniciar Corrida]";
	mes "Por favor, fique do meu lado oriental.";
	menu "Start Race",L_Count,"Cancel",-;
	close;
L_Count:
	set $@counting,1;
	mes "Counting down...";
	addtimer 1000, "Starter::OnCount1000";
	addtimer 2000, "Starter::OnCount2000";
	addtimer 3000, "Starter::OnCount3000";
	addtimer 4000, "Starter::OnCount4000";
	announce strcharinfo(0) + " Iniciou uma contagem regressiva",1;
	announce "Prepare-se para correr!",1;
	close;

OnCount1000:
	announce "[3]",1;
	end;
OnCount2000:
	announce "[2]",1;
	end;
OnCount3000:
	announce "[1]",1;
	end;
OnCount4000:
	emotion ET_GO;
	specialeffect EF_CHIMTO;
	announce "[GO!]",1;
	set $@race,1;
	set $@position,0;
	set $@counting,0;
	set $@raceid,rand(100000,999999);
	end;

L_Started:
	if ((getmapusers("pvp_y_1-5") < 3) && ($@position > 0)) goto L_Menu;
	if ($@position > 2) goto L_Menu;
	mes "[Starter]";
	mes "Corrida em andamento";
	close;

OnInit:
	set $@race,0;
	set $@position,0;
	set $@racecount,0;
	end;
}

//Checkpoint 1
pvp_y_1-5,144,262,5	script	Check Point 1	111,0,5,{
	end;
OnTouch:
	if (@raceid != $@raceid) goto L_Started;
	if (@race == 6) goto L_Finished;
	if ($@race == 1) goto L_Started;
	mes "A corrida não começou, por favor, volte.";
	close;
L_Started:
	set @race,1;
	set @raceid,$@raceid;
	end;
L_Finished:
	mes "Você já completou a corrida.";
	close;
}

//Checkpoint 2
pvp_y_1-5,73,247,5	script	Check Point 2	111,6,6,{
	end;
OnTouch:
	if (@race != 1) goto L_Miss;
	set @race,2;
	announce "[" + strcharinfo(0) +"]chegou ao Checkpoint [1]",1;
	end;
L_Miss:
	mes "Você perdeu um Checkpoint. Por favor, volte.";
	close;
}

//Checkpoint 3
pvp_y_1-5,77,44,5	script	Check Point 3	111,6,6,{
	end;
OnTouch:
	if (@race != 2) goto L_Miss;
	set @race,3;
	announce "[" + strcharinfo(0) +"] chegou ao Checkpoint [2]",1;
	end;
L_Miss:
	mes "Você perdeu um Checkpoint. Por favor, volte.";
	close;
}

//Checkpoint 3
pvp_y_1-5,249,60,5	script	Check Point 4	111,6,6,{
	end;
OnTouch:
	if (@race != 3) goto L_Miss;
	set @race,4;
	announce "[" + strcharinfo(0) +"] chegou ao Checkpoint [3]",1;
	end;
L_Miss:
	mes "Você perdeu um Checkpoint. Por favor, volte.";
	close;
}

//Checkpoint 4
pvp_y_1-5,255,256,5	script	Check Point 5	111,6,6,{
	end;
OnTouch:
	if (@race != 4) goto L_Miss;
	set @race,5;
	announce "[" + strcharinfo(0) +"] chegou ao Checkpoint [4]",1;
	end;
L_Miss:
	mes "Você perdeu um Checkpoint. Por favor, volte.";
	close;
}

//Finish Line
pvp_y_1-5,174,244,5	script	Finish Line	111,6,6,{
	end;
OnTouch:
	if (@raceid != $@raceid) goto L_WrongRace;
	if (@race != 5) goto L_Miss;
	set @race,6;
	set $@position,$@position+1;
	announce "[" + strcharinfo(0) +"] alcançou a linha de chegada! [Posição: " + $@position + "]",1;
	end;
L_Miss:
	mes "Você perdeu um Checkpoint. Por favor, volte.";
	close;
L_WrongRace:
	mes "Você não está nesta corrida.";
	close;
}

//Sinalizadores de ponto de verificação
pvp_y_1-5,144,267,4	script	Check Point 1#01	722,{
	end;
}
pvp_y_1-5,144,257,4	script	Check Point 1#02	722,{
	end;
}
pvp_y_1-5,70,252,3	script	Check Point 2#01	722,{
	end;
}
pvp_y_1-5,77,243,3	script	Check Point 2#02	722,{
	end;
}
pvp_y_1-5,81,48,1	script	Check Point 3#01	722,{
	end;
}
pvp_y_1-5,72,40,1	script	Check Point 3#02	722,{
	end;
}
pvp_y_1-5,244,65,7	script	Check Point 4#01	722,{
	end;
}
pvp_y_1-5,252,57,7	script	Check Point 4#02	722,{
	end;
}
pvp_y_1-5,259,260,5	script	Check Point 5#01	722,{
	end;
}
pvp_y_1-5,251,252,5	script	Check Point 5#02	722,{
	end;
}
pvp_y_1-5,174,249,4	script	Finish Line#01	722,{
	end;
}
pvp_y_1-5,174,238,4	script	Finish Line#02	722,{
	end;
}
