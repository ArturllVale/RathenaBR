// ===== Script rAthena ===========================================
// = Rock Scissors Roulette
// ===== Por: =============================================== =========
// = acky
// ===== Traduzido Por: =============================================== =========
// = Guru
// ===== Versão Atual: ===========================================
// = 1,2
// ===== Compatível com: =========================================
// = Projeto rAthena
// ===== Descrição: ===============================================
// = Joga um jogo híbrido de Roleta Rocha e Tesoura de Papel.
// ===== Comentários adicionais: ===================================
// = Prêmios personalizáveis, emoções adicionadas.
// = 1.2 Correções por Blackthunder e eu [Poki # 3]
//============================================================

cmd_in02,182,126,2	script	Boris louco	85,{
	set .@counter,1;
	mes "Ei você! Quer jogar a roleta Rock Scissors?";
	next;
	menu "• Vamos Jogar",PLAY,"• Explique as regras",RULES,"• Sair",LEAVE;
SAME:
	mes "Desenhar! Novamente!";
	next;
PLAY:
	mes "Pedra... Papel...";
	set .@opp, rand(1,3);
	menu "^0000FFPEDRA!",-,"^FF0000TESOURAS!",SCISSORS,"^00FF00PAPEL!^000000",PAPER;

  	if (.@lastchoice == 1) set .@opp, rand(1,3);
  	if (.@opp == 1) emotion ET_ROCK;
  	else if (.@opp == 2) emotion ET_SCISSOR;
  	else emotion ET_WRAP;
  	set .@lastchoice,1;
	if (.@opp == 1) goto SAME;
	if (.@opp == 2) goto WIN;
	goto LOSE;

SCISSORS:
	if (.@lastchoice == 2) set .@opp,rand(1,2);
  	if (.@opp == 1) emotion ET_ROCK;
  	else if (.@opp == 2) emotion ET_SCISSOR;
  	else emotion ET_WRAP;
  	set .@lastchoice,2;
	if (.@opp == 1) goto LOSE;
	if (.@opp == 2) goto SAME;
	goto WIN;

PAPER:
	if (.@lastchoice == 3) set .@opp,rand(2,3);
  	if (.@opp == 1) emotion ET_ROCK;
  	else if (.@opp == 2) emotion ET_SCISSOR;
  	else emotion ET_WRAP;
  	set .@lastchoice,3;
	if (.@opp == 1) goto WIN;
	if (.@opp == 2) goto LOSE;
	goto SAME;

WIN:
	mes "Droga, você venceu!";
	emotion ET_PROFUSELY_SWEAT;
	next;
	set .@win, 1;
	goto YOUPULL;

LOSE:
	emotion ET_SMILE;
	mes "Boorah! Você perdeu!";
	next;
	set .@win, 0;

YOUPULL:
	mes .@counter +" of 6";
	if ( .@counter == 6 )
		mes "Diga suas orações";
	set .@pull, rand( 1,( 7 -.@counter ) );
	set .@counter, .@counter +1;
	next;
	if ( .@pull == 1 ) {
		if ( .@win ) goto KILL;
		specialeffect2 EF_SUI_EXPLOSION;
		emotion ET_KIK;
		percentheal -100,-100;
		mes "*^0000FFClick^000000* *^FF0000BANG^000000*";
		mes "Você está morto!";
		close;
	}
	emotion ET_HNG;
	mes "*^0000FFClick^000000* ufa...";
	goto PLAY;

RULES:
	mes "Ok, aqui estão as regras:";
	mes "Eu tenho comigo um ^FF00006^000000 pistola de câmara com ^FF00001^000000 volta. Primeiro nós jogamos ^FF0000Tesouras ^00FF00Papel ^0000FFPedra^000000. O perdedor puxa o gatilho. O vencedor é aquele que sair melhor.";
	mes "Vença-me para ganhar um prêmio.";
	next;
	menu "• Vamos Jogar",-,"• Não, Obrigado",LEAVE;
	mes "OK, vamos lá...";
	next;
	goto PLAY;

KILL:
	specialeffect EF_SUI_EXPLOSION;
	emotion ET_HUK;
	mes "*^0000FFClick^000000* *^FF0000BANG^000000*";
	mes "OWWW @#$%^!! ISSO DOE MUITO INFERNO!!";
	next;
	mes "Parabéns! Você venceu...";
	switch( rand( 1,10 ) ) {
		case 1: setarray .@reward[0], 10,984; break;	// Oridecon
		case 3: setarray .@reward[0],100,601; break;	// 100x Asas de Mosca
		case 4: setarray .@reward[0],  8,603; break;	// 8x Velha Caixa Azul
		case 5: setarray .@reward[0],  4,617; break;	// 4x Velha Caixa Roxa
		case 6: setarray .@reward[0],  1,616; break;	// 1x Velho Álbum de Cartas
		case 7: setarray .@reward[0], 10,604; break;	// 10x Galho Seco
		case 8: setarray .@reward[0],  3,969; break;	// 3x Ouro
		case 10: setarray .@reward[0],20,505; break;	// 20x Poção Azul
		case 2:
		case 9:
			setarray .@reward[0], 10,985;
	}
	mes .@reward[0] +"x "+ getitemname( .@reward[1] ) +"!";
	getitem .@reward[1], .@reward[0];
	close;

LEAVE:
	mes "amor-perfeito.";
	close;
}
