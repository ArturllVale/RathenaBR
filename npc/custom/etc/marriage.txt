// ===== Script rAthena ===========================================
// = Casamento Script
// ===== Por: =============================================== =========
// = AppleGirl, Evera
// ===== Traduzido Por: =============================================== =========
// = Guru
// ===== Versão Atual: ===========================================
// = 2,9
// ===== Compatível com: =========================================
// = Projeto rAthena
// ===== Descrição: ===============================================
// = Roteiro de casamento totalmente funcional para todos os tipos de casamentos
// ===== Comentários adicionais: ===================================
// 1.1 Casamentos de lésbicas e gays [ShadowLady]
// 2.0 Reescrita Completa [Skotlex]
// 2.9 Nomes de NPCs um pouco oficiais do iRO [DracoRPG]
//============================================================

// Variáveis ??de configuração:
-	script	marriage_init	-1,{
OnInit:
	set $@wed_allow, 0;	//Se 1, permite casamentos do mesmo sexo.
	set $@wed_veil, 0;	//Defina como 0 para desativar a verificação do véu na noiva

//Id do item que é trocado pela aliança de casamento (use 0 para desativar):
	set $@wed_ring, 2613;

	set $@wed_groom_reg, 1300000;	// Custo de inscrição para o noivo
	set $@wed_bride_reg, 1200000; // Custo de inscrição para a noiva
	set $@wed_divorce_fee, 50000;	// Taxa de divórcio
	set $@wedding_effect, 1; // Sobre quem exibir o FX: 0: Sacerdote, 1: Noiva, 2: Noivo
	end;
}

// Outra configuração:
// Linha 61,62: localização, sprite e nome do sacerdote.
// Linha 437.438: Local de registro, sprite e nome.
// Linha 813.814: Divórcio local, sprite e nome.

// Notas de variáveis:
// $wed_progress	Sinaliza que há um casamento em andamento
// $wed_groom$ - Armazenamento do nome do noivo
// $wed_groom_sex - Sexo do noivo (para o mesmo casamento)
// $wed_bride$ - Armazenamento do nome da noiva
// $wed_bride_sex - Sexo do noivo (para o mesmo casamento)
// $wed_groom_progress - Observa o progresso por parte do noivo
// $wed_bride_progress - Observa o progresso por parte da noiva
// 0: Not registered. 1: Registrado. 2: Aceito o parceiro. 3: Pronto para
// Retrieve the ring. 4: Recuperou o anel. 5: Tudo pronto para se casar. 6: Já
// um casal.
// cerimônia.
// $wedding_effect_id - Quando wedding_effect está habilitado, define o ID do
// jogador para mostrar o efeito.
// $divorce_progress	sinaliza que há um divórcio em andamento
// $@divorcer$ nome da pessoa que pediu o divórcio
// $@divorcee id do parceiro, que deve aceitar o divórcio e pagar.

//O padre - sprite oficial do iRO e nome no diálogo (nome no mapa não confirmado)
prt_church,100,123,4	script	Padre	60,{
	set @name$,"Padre";

	function SF_wed_end;
	function SF_equip_check;
	function SF_Groom;
	function SF_Bride;
	function SF_AcceptGroom;
	function SF_AcceptBride;
	function SF_RetrieveRingM;
	function SF_RetrieveRingF;
	function SF_RingsAccepted;
	function SF_StartCeremony;
	
	if (getpartnerid() > 0) {
		mes "["+@name$+"]";
		mes "Você tem minha bênção, tenha uma vida de casado maravilhosa.";
		close;
	}
	if ($wed_progress == 0) { // Diálogo oficial do iRO
		mes "["+@name$+"]";
		mes "Você deve se inscrever para o";
		mes "casamento com Marry";
		mes "antes que você possa se casar.";
		mes "Marry irá informá-lo";
		mes "o que mais você precisa fazer";
		mes "para se preparar para o casamento";
		close;
	} // Encerrar diálogo oficial do iRO
	
	if (strcharinfo(0) == $wed_groom$) {
		SF_Groom();
		end;
	}
	if (strcharinfo(0) == $wed_bride$) {
		SF_Bride();
		end;
	}
	mes "["+@name$+"]";
	if ($wed_groom_progress == 0 || $wed_bride_progress == 0) {
		mes "Há um casamento sendo planejado. Eu apreciaria se você não me interrompesse.";
		close;
	}
	if ($wed_groom_progress == 6) {
		mes "Eu estou casando "+$wed_groom$+" e "+$wed_bride$+", e já é tarde demais para contestar. Por favor, deixe-me continuar.";
		close;
	}
	mes "Eu vou casar "+$wed_groom$+" e "+$wed_bride$+", você tem alguma objeção?";
	if (select("Desculpe, por favor, continue.","Sim, eu realmente tenho.") == 2) {
		//Abort
		npctalk @name$ +" : Senhoras e senhores, "+strcharinfo(0)+" tem uma objeção ao casamento!";
		SF_wed_end();
		mes "Por que eles não deveriam se casar?";
		input $@msg$;
		npctalk @name$ +" : "+ strcharinfo(0) +"'A objeção é: "+$@msg$;
		emotion ET_CRY;
		mes "Entendo...";
	} else
		mes "Muito bem, sente-se e aproveite a cerimônia.";
	close;

function SF_Groom {
	if ($wed_bride_progress == 0) {
		mes "["+@name$+"]";
		mes "Parece que sua noiva ainda não chegou e se registrou.";
		close;
	}
	if (SF_equip_check() == 0)
		close;

	switch($wed_groom_progress) {
	case 1:
		SF_AcceptBride();
		break;
	case 2:
		mes "["+@name$+"]";
		mes "Estou esperando seu parceiro aceitar você para iniciar a cerimônia.";
		close;
	case 3:
		SF_RetrieveRingM();
		break;
	case 4:
		mes "["+@name$+"]";
		mes "A aliança de casamento do seu parceiro ainda não foi recuperada. A cerimônia começará assim que vocês dois tiverem reclamado seus anéis.";
		close;
	case 5:
		mes "["+@name$+"]";
		SF_StartCeremony();
		break;
	default:
		mes "["+@name$+"]";
		mes "Por favor, não me interrompa agora.";
		close;
	}
}

function SF_Bride {
	if ($wed_groom_progress == 0) {
		mes "["+@name$+"]";
		mes "Parece que seu noivo ainda não chegou e se registrou.";
		close;
	}

	if (SF_equip_check() == 0)
		close;

	switch ($wed_bride_progress) {
	case 1:
		SF_AcceptGroom();
		break;
	case 2:
		mes "["+@name$+"]";
		mes "Estou esperando seu parceiro aceitar você para iniciar a cerimônia.";
		close;
	case 3:
		SF_RetrieveRingF();
		break;
	case 4:
		mes "["+@name$+"]";
		mes "A aliança de casamento do seu parceiro ainda não foi recuperada. A cerimônia começará assim que vocês dois tiverem reclamado seus anéis.";
		close;
	case 5:
		mes "["+@name$+"]";
		SF_StartCeremony();
		break;
	default:
		mes "["+@name$+"]";
		mes "Por favor, não me interrompa agora.";
		close;
	}
}

function SF_AcceptGroom {
	mes "["+@name$+"]";
	mes $wed_bride$+", "+$wed_groom$+" pediu para ser seu marido pelo resto de sua vida. Você aceita?";
	next;
	switch(select("Preciso de tempo para pensar sobre isso.","Não, Eu não aceito!","Sim, Eu aceito!")) {
	case 1:
		mes "["+@name$+"]";
		mes "Você o que!?";
		mes "err .. * tosse * * tosse * muito bem ... volte depois de se decidir.";
		emotion ET_ANGER;
		close;
	case 2:
		mes "["+@name$+"]";
		mes "!!";
		mes "Ah ... err ... ehm ... ok. Vocês dois parecem ter algumas diferenças para resolver primeiro.";
		close2;
		emotion ET_HUK;
		npctalk @name$ +" : Senhoras e senhores, "+$wed_bride$+" rejeitou se casar com "+$wed_groom$+"!";
		SF_wed_end();
		break;
	case 3:
		set $wed_bride_progress,2;
		if ($wed_groom_progress == 2) {
			SF_RingsAccepted();
			break;
		}
		emotion ET_OK;
		mes "["+@name$+"]";
		mes "Depois que seu noivo aprovar, você receberá seu anel, a cerimônia começará e você estará oficialmente casada.";
		close;
	}
}

function SF_AcceptBride {
	mes "["+@name$+"]";
	mes $wed_groom$+", "+$wed_bride$+" pediu para ser sua esposa para o resto de sua vida. Você aceita?";
	next;
	switch(select("Preciso de tempo para pensar sobre isso.","Não, Eu não aceito!","Sim, Eu aceito!")) {
	case 1:
		mes "["+@name$+"]";
		mes "You what!?";
		mes "err .. * tosse * * tosse * muito bem ... volte depois de se decidir.";
		emotion ET_ANGER;
		close;
	case 2:
		mes "["+@name$+"]";
		mes "!!";
		mes "Ah ... err ... ehm ... ok. Vocês dois parecem ter algumas diferenças para resolver primeiro.";
		emotion ET_HUK;
		close2;
		npctalk @name$ +" : Senhoras e senhores, "+$wed_groom$+" rejeitou se casar com "+$wed_bride$+"!";
		SF_wed_end();
		break;
	case 3:
		set $wed_groom_progress,2;
		if ($wed_bride_progress == 2) {
			SF_RingsAccepted();
			break;
		}
		emotion ET_OK;
		mes "["+@name$+"]";
		mes "Depois que sua noiva aprovar, você receberá seu anel, a cerimônia começará e você estará oficialmente casado.";
		close;
	}
}

function SF_RingsAccepted {
	mes "["+@name$+"]";
	mes "Agora que vocês dois aceitaram, o casamento vai começar. Por favor, venha, você e seu parceiro, para recuperar seus anéis.";
	set $wed_bride_progress,3;
	set $wed_groom_progress,3;
	announce $wed_groom$+" e "+$wed_bride$+"' A cerimônia de casamento será realizada na igreja!",8;
	close2;
	emotion ET_THROB;
	npctalk @name$ +" : O noivo e a noiva podem dar um passo à frente e recuperar seus anéis?";
}

function SF_RetrieveRingM {
	mes "["+@name$+"]";
	if ($@wed_ring && countitem($@wed_ring) < 1) {
		mes "O que aconteceu com o seu "+getitemname($@wed_ring)+"? Você não o perdeu ... não é? Precisamos continuar com a cerimônia!";
		close;
	}
	if ($wed_bride_sex)
		set @item, 2634;	//Aliança do noivo
	else
		set @item, 2635;	//Aliança de noiva
	if (getnameditem(@item,$wed_groom$) == 0) {
		mes "Parece que você não tem espaço suficiente para carregar o anel... libere algum espaço e volte para recuperar o anel do seu parceiro.";
		close;
	}
	mes "Aqui está a aliança de casamento para sua noiva.";
	if ($@wed_ring) delitem $@wed_ring,1;
	set $wed_groom_progress,4;
	
	if ($wed_bride_progress == 4)
		SF_StartCeremony();
	else {
		mes "Assim que sua noiva recuperar o anel, a cerimônia começará.";
		close;
	}
}

function SF_RetrieveRingF {
	mes "["+@name$+"]";
	if ($@wed_ring && countitem($@wed_ring) < 1) {
		mes "O que aconteceu com o seu "+getitemname($@wed_ring)+"? Você não o perdeu ... não é? Precisamos continuar com a cerimônia!";
		close;
	}
	if ($wed_groom_sex)
		set @item, 2634;	//Aliança do noivo
	else
		set @item, 2635;	//Aliança da noiva

	if (getnameditem(@item,$wed_bride$) == 0) {
		mes "Parece que você não tem espaço suficiente para carregar o anel... libere algum espaço e volte para recuperar o anel do seu parceiro.";
		close;
	}
	mes "Aqui está a aliança de casamento do seu noivo.";
	if ($@wed_ring) delitem $@wed_ring,1;
	set $wed_bride_progress,4;

	if ($wed_groom_progress == 4)
		SF_StartCeremony();
	else {
		mes "Assim que seu noivo pegar o anel, a cerimônia começará.";
		close;
	}
}

function SF_StartCeremony {
	mes "Iniciarei agora a cerimônia de casamento e vocês serão declarados marido e mulher.";
	set $wed_bride_progress,5;
	set $wed_groom_progress,5;
	set $@msg$,$wed_groom$;
	if (strcharinfo(0) == $wed_groom$)
		set $@msg$,$wed_bride$;
	if (marriage($@msg$) == 0) {
		next;
		mes "["+@name$+"]";
		mes "Onde está "+$@msg$+"?? Eu não posso casar vocês dois se um está faltando...";
		close;
	}
	set $wed_bride_progress,6;
	set $wed_groom_progress,6;
	initnpctimer;
	close;
}

OnTimer1000:
	npctalk @name$ +" : Senhoras e senhores, vamos agora juntar em sagrado matrimônio esses dois amantes.";
	end;
	
OnTimer5000:
	npctalk @name$ +" : Agora, mais do que nunca, suas vidas estarão entrelaçadas, assim como suas almas.";
	end;

OnTimer10000:
	npctalk @name$ +" : Vocês irão honrar e estimar um ao outro nos melhores e nos piores momentos.";
	end;
	
OnTimer15000:
	npctalk @name$ +" : A segurança e o bem-estar do outro também serão de sua responsabilidade.";
	end;
	
OnTimer20000:
	npctalk @name$ +" : Na saúde ou na doença, seu amor queime brilhante como nenhuma força pode apagá-lo.";
	end;
	
OnTimer25000:
	npctalk @name$ +" : Aqueles que estão aqui testemunham esses votos concedidos a você, você deve agir de acordo com eles.";
	end;
	
OnTimer30000:
	npctalk @name$ +" : Compreendendo isso, não somos nada mais do que mortais nesta terra, mas este é o nosso triunfo.";
	end;
	
OnTimer35000:
	npctalk @name$ +" : Nós aqui agora nos uniremos a essas duas entidades mortais e criaremos um amor imortal.";
	end;
	
OnTimer40000:
	npctalk @name$ +" : "+ $wed_groom$ +", você aceitou tomar "+$wed_bride$+" como sua legítima esposa,";
	end;
	
OnTimer45000:
	npctalk @name$ +" : e você, "+$wed_bride$+", aceitaram pegar "+$wed_groom$+" como seu marido legalmente casado.";
	end;

OnTimer50000:
	npctalk @name$ +" : E como tal, agora, pelos poderes investidos em mim...";
	end;

OnTimer55000:
	npctalk @name$ +" : Eu os declaro marido e mulher, você pode beijar a noiva e trocar alianças.";
	if ($wedding_effect_id && isloggedin($wedding_effect_id))
	{
		attachrid($wedding_effect_id);
		wedding;
		detachrid;
	} else
		wedding;
	SF_wed_end();
	stopnpctimer;
	end;

//Subfunção: Verifica se o noivo / noiva ainda está usando suas coisas.
function SF_equip_check {
	if (Sex == SEX_MALE && getequipid(EQI_ARMOR) != 7170) {
		mes "["+@name$+"]";
		mes "Criança, o que você fez com o seu "+getitemname(7170)+"?";
		emotion ET_THINK;
		return 0;
	}
	if (Sex == SEX_FEMALE && getequipid(EQI_ARMOR) != 2338) {
		mes "["+@name$+"]";
		mes "Criança, você deve usar um "+getitemname(2338)+" em todos os momentos durante a cerimônia...";
		emotion ET_THINK;
		return 0;
	}
	if (Sex == SEX_FEMALE && $@wed_veil && getequipid(EQI_HEAD_TOP) != 2206) {
		mes "["+@name$+"]";
		mes "Criança, você não pode tirar seu "+getitemname(2206)+" ainda....";
		emotion ET_THINK;
		return 0;
	}
	return 1;
}

//Subfunção: redefine as variáveis ??do casamento.
function SF_wed_end {
	set $wed_groom$,"";
	set $wed_groom_sex, 0;
	set $wed_bride$,"";
	set $wed_bride_sex, 0;
	set $wed_groom_progress,0;
	set $wed_bride_progress,0;
	set $wed_progress,0;
	set $wedding_effect_id,0;
}

OnInit:
	if ($wed_groom_progress==6) {
		SF_wed_end();
	}
	end;
}

//Status de registro
prt_church,106,99,3	script	Marry	67,{
	set @name$,"Marry";
	if (getpartnerid() > 0) {
		mes "["+@name$+"]";
		mes "O casamento não é lindo?";
		close;
	}
	
	function SF_WedProgress;
	function SF_Principles;
	function SF_Procedure;
	function SF_Register;
	function SF_TryRegister;

	if ($wed_progress) {
		SF_WedProgress();
		end;
	}
	
	do {
		mes "["+@name$+"]";
		mes "Casamento... é uma coisa tão linda.";
		mes "Você gostaria de se casar com alguém?";
		next;
		set @menu, select(
			"[^66ff00•^000000]Eu estarei solteir"+(Sex?"o":"a")+" para sempre!",
			"[^66ff00•^000000]Explique os princípios do casamento.",
			"[^66ff00•^000000]Explique o procedimento de casamento.",
			"[^66ff00•^000000]Eu quero me casar com alguém."
		);
		switch (@menu) {
		case 1: //Sair
			mes "["+@name$+"]";
			mes "Nesse caso, aproveite a vida de solteir"+(Sex?"o":"a")+".";
			close;
		case 2: //Princípios
			SF_Principles();
			break;
		case 3: //Procedimento
			SF_Procedure();
			break;
		case 4: //Registro
			SF_Register();
			break;
		}
	} while (@menu > 1);
	end;

function SF_Register {
	if ($@wed_allow) { //Seleção de função
		mes "["+@name$+"]";
		mes "Muito bem, como você gostaria de se registrar?";
		next;
		set @submenu, select("[cc0000Noivo^000000]","^ff3399Noiva^000000","^666666Cancelar^000000");
	} else if (Sex == SEX_MALE) { //Noivo
		mes "["+@name$+"]";
		mes "Muito bem, você vai se registrar como o noivo?";
		next;
		if (select("^66cc66Sim^000000","^993333Mudei de ideia.^000000")==1)
			set @submenu, 1;
		else
			set @submenu, 3;
	} else { //Noiva
		mes "["+@name$+"]";
		mes "Muito bem, você vai se registrar como a Noiva?";
		next;
		if (select("^66cc66Sim^000000","^993333Mudei de ideia.^000000")==1)
			set @submenu, 2;
		else
			set @submenu, 3;
	}
	switch (@submenu) {
	case 1: //Noivo
		SF_TryRegister(0);
		set $wed_progress,1;
		mes "["+@name$+"]";
		mes "Agora você está registrado como noivo.";
		mes "Diga a sua noiva para se registrar o mais rápido possível.";
		emotion ET_SCRATCH;
		initnpctimer;
		close;
	case 2: //Noiva
		SF_TryRegister(1);
		set $wed_progress,1;
		mes "["+@name$+"]";
		mes "Você agora está registrado como noiva.";
		mes "Diga ao seu noivo para se registrar o mais rápido possível.";
		emotion ET_SCRATCH;
		initnpctimer;
		close;
	default: //Cancelar
		mes "["+@name$+"]";
		mes "Volte quando estiver pront"+(Sex?"o":"a")+".";
		close;
	}
}
	
function SF_WedProgress {
	if (strcharinfo(0) == $wed_groom$) {
		mes "["+@name$+"]";
		if ($wed_bride_progress > 0)
			mes "O Padre cuidará do resto da cerimônia.";
		else
			mes "Diga a sua noiva para se registrar, por que está demorando tanto? O tempo está se esgotando.";
		close;
	}
	if (strcharinfo(0) == $wed_bride$) {
		mes "["+@name$+"]";
		if ($wed_groom_progress > 0) 
			mes "O Padre cuidará do resto da cerimônia.";
		else
			mes "Diga ao seu noivo para se registrar, por que está demorando tanto? O tempo está se esgotando.";
		close;
	}
	if (($wed_groom_progress == 0) && (Sex == SEX_MALE || $@wed_allow == 1)) {
		mes "["+@name$+"]";
		mes $wed_bride$+" está esperando o noivo se registrar. É você quem veio se registrar como noivo?";
		next;
		if (select("^336633Sim, eu sou.^000000","^666666Desculpe, pessoa errada.^000000") == 1) {
			SF_TryRegister(0);
			stopnpctimer;
			set $wed_groom_progress,1;
			mes "["+@name$+"]";
			mes "Muito bem, agora vá ao Padre para reafirmar seus votos e a cerimônia terá início."; 
			emotion ET_BEST;
			close2;
			npctalk @name$ +" : Registro concluído. "+$wed_groom$+" and "+$wed_bride$+", por favor, reafirme seus votos com o Padre.";
			emotion ET_BEST;
			end;
		} else {
			mes "["+@name$+"]";
			mes "Entendo. Desculpe incomodá-l"+(Sex?"o":"a")+" então.";
			close;
		}

	}
	if (($wed_bride_progress == 0) && (Sex == SEX_FEMALE || $@wed_allow == 1)) {
		mes "["+@name$+"]";
		mes $wed_groom$+" está esperando a noiva se registrar. Foi você quem veio se registrar como noiva?";
		next;
		if(select("^336633Sim, eu sou.^000000","^666666Desculpe, pessoa errada.^000000") == 1) {
			SF_TryRegister(1);
			stopnpctimer;
			mes "["+@name$+"]";
			mes "Muito bem, agora vá ao Padre para reafirmar seus votos e a cerimônia terá início."; 
			emotion ET_BEST;
			close2;
			npctalk @name$ +" : Registro concluído. "+$wed_groom$+" and "+$wed_bride$+", por favor, reafirme seus votos com o Padre.";
			emotion ET_BEST;
			end;
		} else {
			mes "["+@name$+"]";
			mes "Entendo. Desculpe incomodá-l"+(Sex?"o":"a")+" então.";
			close;
		}
	}
	mes "["+@name$+"]";
	mes "Há um casamento em andamento.";
	mes "Gostaria de saber o andamento desse casamento?";
	next;
	if (select("• Sim","• Não") != 1) {
		mes "["+@name$+"]";
		mes "Aproveite o casamento.";
		close;
	}
	//Progresso de exibição
	mes "["+@name$+"]";
	switch ($wed_groom_progress) {
	case 0:
		mes "O noivo ainda não se inscreveu.";
		break;
	case 1:
		mes "O noivo, "+$wed_groom$+", ainda não aceitou a noiva.";
		break;
	case 2:
		mes "O noivo, "+$wed_groom$+", está esperando a aceitação da noiva.";
		break;
	case 3:
		mes "O noivo, "+$wed_groom$+", ainda não recuperou o anel.";
		break;
	case 4:
		mes "O noivo, "+$wed_groom$+", está esperando que a noiva recupere o anel.";
		break;
	}
	switch ($wed_bride_progress) {
	case 0:
		mes "A noiva ainda não se cadastrou.";
		break;
	case 1:
		mes "A noiva, "+$wed_bride$+", ainda não aceitou o noivo.";
		break;
	case 2:
		mes "A noiva, "+$wed_bride$+", está esperando a aceitação do noivo.";
		break;
	case 3:
		mes "A noiva, "+$wed_bride$+", ainda não recuperou o anel.";
		break;
	case 4:
		mes "A noiva, "+$wed_bride$+", está esperando que o noivo recupere o anel.";
		break;
	case 5:
		mes "Estamos apenas esperando por ambos "+$wed_groom$+" e "+$wed_bride$+" estar juntos para casar eles.";
		break;
	case 6:
		mes $wed_groom$+" e "+$wed_bride$+"' A cerimônia de casamento já está bem encaminhada.";
		break;
	}
	mes "Aproveite o restante do casamento.";
	close;
}

OnInit:
	if ($wed_groom_progress + $wed_bride_progress == 1)
		initnpctimer;
	end;

OnTimer60000:
	//Registro falhou.
	if ($wed_bride_progress == 1)
		set $@msg$, $wed_bride$;
	else
		set $@msg$, $wed_groom$;

	npctalk @name$ +" : O registro expirou. Não há ninguém que queira casar "+$@msg$+"..?";
	emotion ET_SCRATCH;
	
	set $wed_groom$,"";
	set $wed_groom_sex, 0;
	set $wed_bride$,"";
	set $wed_bride_sex, 0;
	set $wed_groom_progress,0;
	set $wed_bride_progress,0;
	set $wed_progress,0;
	stopnpctimer;
	end;

//Subfunção SF_ExperimenteRegister (int noiva)
function SF_TryRegister {
	set @bride, getarg(0);
	set @type$, "noivo";
	if (@bride)
		set @type$, "noiva";
		
	mes "["+@name$+"]";
	mes "Antes de se registrar como "+@type$+", deixe-me verificar se você atende a todos os requisitos ...";
	next;
	if (Upper == 2) {
		mes "["+@name$+"]";
		mes "Nossa, você é muito jovem para pensar em casamento!";
		emotion ET_SURPRISE;
		close;
	}
	if (Sex == SEX_MALE)
		set @item, 7170;
	else
		set @item, 2338;
	
	if (getequipid(EQI_ARMOR) != @item) {
		mes "["+@name$+"]";
		mes "Você deveria estar usando um "+getitemname(@item)+" se você quer se casar.";
		close;
	}
	if (Sex == SEX_FEMALE && $@wed_veil && getequipid(EQI_HEAD_TOP) != 2206) {
		mes "["+@name$+"]";
		mes "Onde está seu "+getitemname(2206)+"? É um complemento necessário ao seu vestido.";
		close;
	}
	if ($@wed_ring && countitem($@wed_ring) < 1) {
		mes "["+@name$+"]";
		mes "Onde está o anel? Você precisa de um "+getitemname($@wed_ring)+" para a troca de anel, querida.";
		close;
	}
	if (@bride)
		set @cost, $@wed_bride_reg;
	else 
		set @cost, $@wed_groom_reg;

	if (Zeny < @cost) {
		mes "["+@name$+"]";
		mes "Sinto muito, mas você não tem o suficiente para pagar a taxa de registro.";
		mes "Volte depois de coletar "+@cost+"z."; 
		close;
	}
	set Zeny,Zeny-@cost;
	sc_start SC_Wedding,3600000,1; //Iniciar efeito de casamento (SC_WEDDING)
	if (@bride) {
		set $wed_bride_progress,1;
		set $wed_bride$,strcharinfo(0);
		set $wed_bride_sex, Sex;
		if ($@wedding_effect == 1) //ID da conta da loja para efeito.
			set $wedding_effect_id, getcharid(3);
	} else {
		set $wed_groom_progress,1;
		set $wed_groom$,strcharinfo(0);
		set $wed_groom_sex, Sex;
		if ($@wedding_effect == 2) //ID da conta da loja para efeito.
			set $wedding_effect_id, getcharid(3);
	}
}

//Explique os princípios do casamento...
function SF_Principles {
	mes "["+@name$+"]";
	mes "Os casamentos são realizados pelo nosso padre local com a intenção de promover o amor e a paz entre os casais apaixonados.";
	next;
	mes "["+@name$+"]";
	mes "A proposta deve ser feita com prudência e cortesia, uma vez feito os casamentos, eles não podem ser desfeitos.";
	next;
	mes "["+@name$+"]";
	mes "Os dois que foram unidos pelo casamento devem permanecer juntos para sempre até o dia em que a morte os separe.";
	next;
	mes "["+@name$+"]";
	if ($@wed_allow == 1)
		mes "Embora normalmente apenas os homens possam casar com mulheres (e vice-versa), nosso sacerdote local é mais aberto do que isso e permite todos os pares, independentemente do sexo.";
	else
		mes "Homens só podem casar com mulheres, e mulheres apenas com homens; a igreja não consentirá qualquer outro tipo de parceria.";
	next;
	mes "["+@name$+"]";
	mes "Se houver uma pessoa importante com quem você deseja passar o resto de sua vida, não tenha vergonha de propor.";
	next;
	mes "["+@name$+"]";
   mes "Desejo muitas bênçãos aos casais que desejam viver felizes para sempre...";
	next;
}

//Explique o procedimento do casamento ...
function SF_Procedure {
	mes "["+@name$+"]";
	mes "Em primeiro lugar, tanto o noivo quanto a noiva devem se registrar comigo.";
	next;
	if ($@wed_allow == 1) {
		mes "["+@name$+"]";
		mes "Os requisitos de registro são:";
		mes "- Os Homens devem estar usando um "+getitemname(7170)+".";
		mes "- As mulheres devem estar usando um "+getitemname(2338)+".";
		if ($@wed_veil) mes "- As mulheres também devem usar um "+getitemname(2206)+".";
		if ($@wed_ring) mes "- Ambos devem possuir um "+getitemname($@wed_ring)+" cada.";
		if ($@wed_groom_reg > 0) mes "- A taxa de inscrição para o noivo é "+$@wed_groom_reg+"z.";
		if ($@wed_bride_reg > 0) mes "- A taxa de inscrição para a noiva é "+$@wed_bride_reg+"z.";
	} else {
		mes "["+@name$+"]";
		mes "Os requisitos de registro para o noivo são:";
		mes "- Estar usando um "+getitemname(7170)+".";
		if ($@wed_ring) mes "- Para possuir um "+getitemname($@wed_ring)+".";
		if ($@wed_groom_reg > 0) mes "- Pagar uma taxa de registro de "+$@wed_groom_reg+"z.";
		next;
		mes "["+@name$+"]";
		mes "Os requisitos de registro para a noiva são:";
		mes "- Estar vestindo um "+getitemname(2338)+".";
		if ($@wed_veil) mes "- Estar vestindo um "+getitemname(2206)+".";
		if ($@wed_ring) mes "- Para possuir um "+getitemname($@wed_ring)+".";
		if ($@wed_bride_reg > 0) mes "- Pagar uma taxa de registro de "+$@wed_bride_reg+"z.";
	}
	next;
	mes "["+@name$+"]";
	mes "Eu não deveria precisar mencionar isso, mas filhos adotivos são muito jovens para se casar.";
	mes "O noivo e a noiva devem se registrar com um minuto de diferença, ou o casamento será cancelado. Portanto, certifique-se de que ambos estão prontos e atendem aos requisitos de registro com antecedência.";
	next;
	mes "["+@name$+"]";
	mes "Depois de ambos se registrarem comigo, você tem que fazer seus votos ao Padre e aceitar seu parceiro registrado. Se por algum motivo você rejeitar seu parceiro registrado, o casamento será cancelado...";
	next;
	mes "["+@name$+"]";
	mes "Se vocês dois se aceitarem, o casamento estará decidido e a cerimônia começará.";
	if ($@wed_ring) {
		mes "Mas primeiro, você precisa preparar os anéis.";
		next;
		mes "["+@name$+"]";
		mes "Fale com o padre mais uma vez, e ele vai trocar o seu "+getitemname($@wed_ring)+" para uma aliança de casamento. Depois que vocês dois tiverem reivindicado os anéis para troca, a cerimônia começará.";
	}
	next;
	mes "["+@name$+"]";
	mes "Se houver vários casais que desejam se casar, você deve manter a ordem, pois o sacerdote só pode cuidar de um casamento de cada vez.";
	next;
}
}

prt_church,94,99,4	script	Irmã Lisa	79,{
	set @name$,"Lisa";

	function SF_DivorceEnd;
	function SF_InProgress;
	
	if ($@divorce_progress==1) {
		goto SF_InProgress;
		end;
	}
	
	do {
		mes "["+@name$+"]";
		mes "O divórcio pode ser um acontecimento tão triste...";
		if (getpartnerid() == 0) {
			mes "As pessoas não devem fazer votos rasos para os outros, você não acha?";
			close;
		}
		mes "Você não iria querer se divorciar, por acaso?";
		next;
		set @menu, select(
			"[^006600•^000000] Estou feliz como estou, obrigado.",
			"[^006600•^000000] Explique o divórcio.",
			"[^006600•^000000] Explique os requisitos.",
			"[^006600•^000000] Quero me divorciar."
		);
		switch (@menu) {
		case 1:
			mes "["+@name$+"]";
			mes "Bom de se ouvir.";
			close;
		case 2: //Explicação
			mes "["+@name$+"]";
			mes "Mesmo que se diga que uma vez que o casamento tenham sido feito, ele não podem ser desfeito, às vezes é necessário desfazer nossos erros do passado.";
			next;
			mes "["+@name$+"]";
			mes "É triste mas verdade. Se por acaso você se casou com a pessoa errada, é possível se divorciar, em vez de passar o resto da vida com a pessoa errada.";
			next;
			break;
		case 3: //Requerimento
			mes "["+@name$+"]";
			mes "Para pedir o divórcio, preciso que vocês dois concordem.";
			mes "Depois que você pede o divórcio, seu cônjuge tem um minuto para concordar e, então, vocês dois se divorciarão.";
			if ($@wed_divorce_fee > 0) mes "A taxa é de "+$@wed_divorce_fee+"z e é pago pela pessoa que confirma o divórcio, então planeje com antecedência como você dividirá os custos.";
			next;
			break;
		case 4: //Divórcio
			mes "["+@name$+"]";
			mes "Você não deve se arrepender das escolhas que fez na vida.";
			mes "Você tem certeza absoluta de que vai se divorciar?";
			next;
			if (select("Espere... eu preciso pensar sobre isso.","Absolutamente") != 2) {
				mes "["+@name$+"]";
				mes "Você deve pensar nisso.";
				close;
			} 
			mes "["+@name$+"]";
			set $@divorcee,getpartnerid();
			set $@divorcer$,strcharinfo(0);
			set $@divorce_progress,1;
			initnpctimer;
			mes "Muito bem, peça ao seu parceiro para confirmar, e irei cobrar a taxa para a apresentação o divórcio.";
			close;
		}
	} while (@menu > 1);
end;

function SF_InProgress {
	if (strcharinfo(0) == $@divorcer$) { 
		mes "["+@name$+"]";
		mes "...Ainda estou esperando que seu parceiro confirme o processo de divórcio.";
		close;
	}
	if (getcharid(0) != $@divorcee) {
		mes "["+@name$+"]";
		mes "Estou em vias de me divorciar "+$@divorcer$+".";
		mes "Você sabe quem é o cônjuge?";
		close;
	}
	//Confirm...
	mes "["+@name$+"]";
	mes $@divorcer$+" pediu para se divorciar de você. Se você aceitar, e tiver a taxa de "+$@wed_divorce_fee+"z em mãos, vou proceder ao divórcio de vocês dois.";
	mes "Então, devo prosseguir com o divórcio?";
	next;
	if (select("Eu não quero me divorciar....","Sim, concordamos com isso.")!=2) {
		mes "["+@name$+"]";
		mes "Eu espero que você possa resolver as coisas.";
		emotion ET_GOODBOY;
		goto SF_DivorceEnd;
		close;
	}
	if (Zeny < $@wed_divorce_fee) {
		mes "["+@name$+"]";
		mes "Bem, não posso pedir o seu divórcio porque você não tem dinheiro suficiente para pagar a taxa. Peça ao seu parceiro para lhe emprestar algum?";
		close;
	}
	if (!(divorce())) {
		mes "["+@name$+"]";
		mes "Onde seu "+$@divorcer$+" foi para? Eu não posso me divorciar de você a menos que vocês dois estejam aqui ...";
		emotion ET_PROFUSELY_SWEAT;
		close;
	}
	set Zeny,Zeny-$@wed_divorce_fee;
	announce $@divorcer$+" acaba de se divorciar de "+strcharinfo(0)+"...", 8;
	mes "["+@name$+"]";
	mes "Seu divórcio foi arquivado. Você não está mais casad"+(Sex?"o":"a")+".";
	emotion ET_CRY;
	goto SF_DivorceEnd;
	close;
}

function SF_DivorceEnd {
	stopnpctimer;
	set $@divorce_progress,0;
	set $@divorcee,0;
	set $@divorcer$,"";
}

OnTimer60000:
	npctalk @name$ +" : O tempo de confirmação do divórcio acabou. Onde fez "+$@divorcer$+"'a esposa vai...";
	emotion ET_QUESTION;
	SF_DivorceEnd();
	end;
}
