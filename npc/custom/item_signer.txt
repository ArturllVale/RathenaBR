// ===== Script rAthena ===========================================
// = Assine seus itens
// ===== Por: =============================================== =========
// = Lupus
// ===== Traduzido Por: =============================================== =========
// = Guru
// ===== Versão Atual: ===========================================
// = 1.1a
// ===== Compatível com: =========================================
// = Projeto rAthena
// ===== Descrição: ===============================================
// = Escreva seu nome em seu equipamento raro ou arma ^ _-
// ===== Comentários adicionais: ===================================
// = 1.1 Limpo e editado para uso geral. [Euphy]
// = 1.1a Adicionado comando 'disable_items'. [Euphy]
//============================================================

prt_in,24,61,7	script	Perchik	47,{
	
	setarray .@Item[0],644,3;	// Requisitos do item: <ID>,<quantidade>{,...} (0 desabilitar)
	setarray .@Cost[0],0,5000;	// Requisitos Zeny: <preço base>,<preço por refinar>

	disable_items;
	mes "[Perchik]";
	mes "Olá senh"+(Sex?"or":"ora")+", eu posso ^0055FFassinar seu nome^000000 em quase todos item raro que você tiver.";
	next;
	if(select("Me diga mais.:Assine meus itens, por favor!") == 1) {
		mes "[Perchik]";
		mes "Posso colocar seu nome em qualquer equipamento ou arma sem slot.";
		emotion ET_AHA;
		next;
		mes "[Perchik]";
		if (getarraysize(.@Item) || getarraysize(.@Cost)) {
			mes "Pelo meu trabalho eu aceito:";
			if (getarraysize(.@Item)) for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2)
				mes "[^330099»^000000] "+.@Item[.@i+1]+"x "+getitemname(.@Item[.@i]);
			if (.@Cost[0]) mes " "+callfunc("F_InsertComma",.@Cost[0])+" Zeny";
			if (.@Cost[1]) mes "[^FFFF33$^000000] "+callfunc("F_InsertComma",.@Cost[1])+" Zeny por refino";
		}
		else mes "Trabalho de graça, mas...";
		next;
		emotion ET_CRY;
		mes "[Perchik]";
		mes "Infelizmente, tenho 12 filhos famintos";
		mes "e uma esposa muito zangada.";
		next;
		mes "[Perchik]";
		mes "Ou foram 12 crianças zangadas";
		mes "e uma esposa com muita fome...";
		emotion ET_SCRATCH;
		close;
	}
	mes "[Perchik]";
	mes "Mostre-me os itens para assinar...";
	next;
	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for (set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@indices[.@i]))
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) +" [^0055FF"+getequipname(.@indices[.@i])+"^000000]";
		set .@menu$, .@menu$+":";
	}
	set .@part, .@indices[ select(.@menu$) ];
	set .@id, getequipid(.@part);
	set .@ref, getequiprefinerycnt(.@part);
	mes "[Perchik]";
	if (!getequipisequiped(.@part)) {
		mes "Não tem nada equipado!";
		emotion ET_KEK;
		close;
	}
	for(set .@i,0; .@i<4; set .@i,.@i+1)
		set .@slot[.@i], getequipcardid(.@part,.@i);
	if (.@slot[0]==255 || .@slot[0]==254 || .@slot[0]<0) {
		mes "Infelizmente, este item já está assinado.";
		mes "Eu nunca tocaria na obra de um mestre.";
		emotion ET_SCRATCH;
		close;
	}
	if (.@slot[0]>4000 && .@slot[0]<5000) {
		mes "Uma carta? Aqui?!";
		mes "Como disse antes, não assino itens com cartas.";
		emotion ET_SCRATCH;
		close;
	}
	if (getiteminfo(.@id,10)) {
		mes "Desculpe, eu não assino itens com slot.";
		emotion ET_SORRY;
		close;
	}
	getinventorylist;
	for(set .@i,0; .@i<@inventorylist_count; set .@i,.@i+1)
		if (@inventorylist_expire[.@i] != 0) {
			mes "Desculpe, não assino itens de aluguel!";
			emotion ET_SCRATCH;
			close;
		}
	set .@price, .@Cost[0]+(.@Cost[1]*.@ref);
	if (getarraysize(.@Item) || .@price) {
		mes "Eu vou precisar:";
		if (getarraysize(.@Item)) for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2) {
			mes "[^330099»^000000] "+.@Item[.@i+1]+"x "+getitemname(.@Item[.@i]);
			if (countitem(.@Item[.@i]) < .@Item[.@i+1]) set .@nr,1;
		}
		if (.@price) mes "[^330099»^000000] "+.@price+" Zeny";
	}
	mes "Devo assinar o seu ^0055FF"+getitemname(.@id)+"^000000?";
	next;
	if(select("Ok!:Cancelar")==2) {
		mes "[Perchik]";
		mes "Até logo...";
		emotion ET_SLEEPY;
		close;
	}
	mes "[Perchik]";
	if (Zeny < .@price || .@nr) {
		mes "Eu não trabalho para 'Obrigado'.";
		emotion ET_SORRY;
		close;
	}
	if (!countitem2(.@id,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3])) {
		mes "Onde é "+getitemname(@id)+"...?";
		npctalk "Perchik: Você é um trapaceiro sonolento!";
		logmes "Hack: Tentei assinar um item sem tê-lo: "+getitemname(@id);
		emotion ET_KEK;
		close;
	}
	if (.@price) set Zeny, Zeny-.@price;
	if (getarraysize(.@Item))  for(set .@i,0; .@i<getarraysize(.@Item); set .@i,.@i+2)
		delitem .@Item[.@i], .@Item[.@i+1];
	delitem2 .@id,1,1,.@ref,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3];
	mes "Está feito!";
	emotion ET_OK;
	getitem2 .@id,1,1,.@ref,0,254,0,getcharid(0)&0xffff,(getcharid(0)>>16)&0xffff;
	equip .@id;
	close;
}
