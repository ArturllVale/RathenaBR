// ===== Script rAthena ===========================================
// = NPC de locação universal
// ===== Por: =============================================== =========
// = Euphy
// ===== Traduzido Por: =============================================== =========
// = Guru
// ===== Versão Atual: ===========================================
// = 1.0a
// ===== Compatível com: =========================================
// = Projeto rAthena
// ===== Descrição: ===============================================
// = Aluguel de montagem automática com um clique.
//============================================================

prontera,124,201,1	script	Garota Negociante	726,{
	if (ismounting()) {
		message strcharinfo(0),"Você deve remover sua montaria.";
		end;
	} else if (((eaclass()&EAJ_THIRDMASK)==EAJ_RANGER) && !countitem(6124)) {
		if (!checkfalcon() && getskilllv("HT_FALCON") && !checkwug()) {
			if(select(" ~ Falcon: ~ Warg")==1) setfalcon;
			else getitem 6124,1; //Flauta de lobo
		} else getitem 6124,1; //Flauta de lobo
	} else if (((eaclass()&EAJ_THIRDMASK)==EAJ_MECHANIC) && !checkcart() && getskilllv("MC_PUSHCART")) {
		if (!checkmadogear() && getskilllv("NC_MADOLICENCE")) {
			if(select(" ~ Cart: ~ Mado")==1) setcart;
			else setmadogear;
		} else setcart;
	} else if (!checkcart() && getskilllv("MC_PUSHCART")) setcart;
	else if (!checkfalcon() && getskilllv("HT_FALCON") && !checkwug()) setfalcon;
	else if (!checkriding() && !checkdragon() && getskilllv("KN_RIDING")) {
		if ((eaclass()&EAJ_THIRDMASK)==EAJ_RUNE_KNIGHT) setdragon;
		else setriding;
	} else if (!checkmadogear() && getskilllv("NC_MADOLICENCE")) setmadogear;
	else {
		message strcharinfo(0),"Você não atende aos requisitos para alugar.";
		end;
	}
	specialeffect2 EF_TEIHIT3;
	end;
}
