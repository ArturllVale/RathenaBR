// ===== Script rAthena ===========================================
// = Campo de batalha: PVP
// ===== Por: =============================================== =========
// = AnnieRuru
// ===== Traduzido Por: ===============================================
// = Guru
// ===== Versão Atual: ===========================================
// = 1,1
// ===== Compatível com: =========================================
// = Projeto rAthena
// ===== Descrição: ===============================================
// = Um script de campo de batalha simples:
// = Mate jogadores do outro time.
// ===== Comentários adicionais: ===================================
// = 1.0 Primeira versão, editada. [Euphy]
// = 1.1 Use comandos de script de campo de batalha atualizados [AnnieRuru]
//============================================================

-	script	bg_pvp#control	-1,{
OnInit:
	.minplayer2start = 2;      // jogadores mínimos para começar (por exemplo, se 3vs3, defina como 3)
	.eventlasting    = 20*60;  // duração do evento antes da reinicialização automática (20 minutos * segundos)
	setarray .rewarditem[0],   // recompensas para a equipe vencedora: <item>, <quantidade>, ...
		501, 10;
	end;
OnStart:
	if ( getwaitingroomstate( 0, .rednpcname$ ) < .minplayer2start || getwaitingroomstate( 0, .bluenpcname$ ) < .minplayer2start )
		end;

	// criar campo de batalha e equipes
	.red = waitingroom2bg( "guild_vs3", 13,50, strnpcinfo(0)+"::OnRedQuit", strnpcinfo(0)+"::OnRedDead", .rednpcname$ );
	.blue = waitingroom2bg( "guild_vs3", 86,50, strnpcinfo(0)+"::OnBlueQuit", strnpcinfo(0)+"::OnBlueDead", .bluenpcname$ );
	delwaitingroom .rednpcname$;
	delwaitingroom .bluenpcname$;
	bg_warp .red, "guild_vs3", 13,50;
	bg_warp .blue, "guild_vs3", 86,50;
	.red_score = .blue_score = .minplayer2start;
	bg_updatescore "guild_vs3", .red_score, .blue_score;

	// duração da partida
	sleep .eventlasting * 1000;

	// termine a partida, destrua o campo de batalha, reinicie os NPCs
	if ( .red_score > .blue_score ) {
		mapannounce "guild_vs3", "- A equipe vermelha Venceu! -", bc_map;
		callsub L_Reward, .red;
	}
	else if ( .blue_score > .red_score ) {
		mapannounce "guild_vs3", "- A equipe Azul Venceu! -", bc_map;
		callsub L_Reward, .blue;
	}
	else
		mapannounce "guild_vs3", "- A partida terminou em empate! -", bc_map;
	bg_warp .red, "prontera",152,178;
	bg_warp .blue, "prontera",154,178;
	bg_destroy .red;
	bg_destroy .blue;
	donpcevent .rednpcname$ +"::OnStart";
	donpcevent .bluenpcname$ +"::OnStart";
	end;

L_Reward:
	bg_get_data getarg(0), 1;
	for ( .@i = 0; .@i < $@arenamemberscount; ++.@i )
		getitem .rewarditem[0], .rewarditem[1], $@arenamembers[.@i];
	return;

// "OnDeath" event
OnRedDead:  callsub L_Dead, .red_score;
OnBlueDead: callsub L_Dead, .blue_score;
L_Dead:
	set getarg(0), getarg(0) -1;
	bg_updatescore "guild_vs3", .red_score, .blue_score;
	bg_leave;
	if ( !getarg(0) )
		awake strnpcinfo(0);
	sleep2 1250;
	percentheal 100,100;
	end;

// "OnQuit" event
OnRedQuit:  callsub L_Quit, .red_score;
OnBlueQuit: callsub L_Quit, .blue_score;
L_Quit:
	set getarg(0), getarg(0) -1;
	bg_updatescore "guild_vs3", .red_score, .blue_score;
	percentheal 100, 100;
	if ( !getarg(0) )
		awake strnpcinfo(0);
	end;
}

prontera,152,178,5	script	Equipe Vermelha#bg_pvp	733,{
	end;
OnInit:
	sleep 1;
	set getvariableofnpc( .rednpcname$, "bg_pvp#control" ), strnpcinfo(0);
OnStart:
	waitingroom "Equipe Vermelha", getvariableofnpc( .minplayer2start, "bg_pvp#control" ) +1, "bg_pvp#control::OnStart", getvariableofnpc( .minplayer2start, "bg_pvp#control" );
	end;
}

prontera,154,178,5	script	Equipe Azul#bg_pvp	734,{
	end;
OnInit:
	sleep 1;
	set getvariableofnpc( .bluenpcname$, "bg_pvp#control" ), strnpcinfo(0);
OnStart:
	waitingroom "Equipe Azul", getvariableofnpc( .minplayer2start, "bg_pvp#control" ) +1, "bg_pvp#control::OnStart", getvariableofnpc( .minplayer2start, "bg_pvp#control" );
	end;
}

guild_vs3	mapflag	gvg	off
guild_vs3	mapflag	battleground	2
guild_vs3	mapflag	nosave	SavePoint
guild_vs3	mapflag	nowarp
guild_vs3	mapflag	nowarpto
guild_vs3	mapflag	noteleport
guild_vs3	mapflag	nomemo
guild_vs3	mapflag	nopenalty
guild_vs3	mapflag	nobranch
guild_vs3	mapflag	noicewall
guild_vs3	mapflag	hidemobhpbar
