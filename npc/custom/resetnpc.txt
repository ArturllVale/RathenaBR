// ===== Script rAthena ===========================================
// = Reiniciar NPC
// ===== Descrição: ===============================================
// = Redefine habilidades, estatísticas ou ambos.
// ===== Traduzido por: ===============================================
// = Guru
// ===== Comentários adicionais: ===================================
// = 1.0 Primeira versão
// = 1,1 Otimizado para o bem maior. [Kisuka]
// = 1.2 Limpeza [Euphy]
// = 1,3 Todos os status removidos na reinicialização da habilidade. [Euphy]
// = 1.4 Script compactado, opção de uso de limite adicionada [Stolao]
// = Conjunto alterado -> setarray, texto aprimorado com F_InsertPlural
// = 1.5 Adicionado sc_end_class para redefinir as alterações de status relacionadas [sader1992]
//============================================================
prontera,150,193,4	script	Resetadora	124,{
	//	 		Skills,	Stats,	Both,	Limit
	setarray .@Reset,	5000,	5000,	9000,	0;
	mes "[Resetadora]";
	if(.@Reset[3] && reset_limit > .@Reset[3]) {
		mes "Desculpe, você só pode redefinir "+callfunc("F_InsertPlural",.@Reset[3],"time")+" na sua vida.";
		close;
	}
	mes "Eu sou a Resetadora.";
	mes "^660000Resetar Status:^000000 "+ callfunc("F_InsertComma",.@Reset[1]) +"z";
	mes "^000033Resetar Habilidades:^000000 "+ callfunc("F_InsertComma",.@Reset[0]) +"z";
	mes "^333333Resetar Ambos:^000000 "+ callfunc("F_InsertComma",.@Reset[2]) +"z";
	if(.@Reset[3]) mes "Você só pode redefinir "+callfunc("F_InsertPlural",.@Reset[3],"time")+", então use "+((.@Reset[3]>1)?"eles":"isto")+" wisely.";
	mes "Selecione o serviço que deseja:";
	next;
	set .@i,(select("[»] ^000033Resetar Habilidades^000000:[»] ^660000Resetar Status^000000:[»] ^333333Resetar Ambos^000000:[«] Cancelar"));
	if(.@i > 3) close;
	mes "[Resetadora]";
	if (Zeny < .@Reset[.@i-1]) {
		mes "Desculpe, você não tem Zeny suficiente.";
		close;
	}
	if(.@Reset[3]){
		mes "Você só pode redefinir "+callfunc("F_InsertPlural",.@Reset[3],"time")+" na sua vida tem certeza?";
		if(select("Deixe-me pensar:Isso é bom") == 1) close;
	}
	set Zeny, Zeny-.@Reset[.@i-1];
	if(.@i&1){
		sc_end_class;
		ResetSkill;
	}
	if(.@i&2) ResetStatus;
	mes "Ai está!";
	if(.@Reset[3]) set reset_limit,reset_limit + 1;
	close;
}
