// ===== Script rAthena ===========================================
// = Vendedor de carta A-Z
// ===== Traduzido por: ===============================================
// = Guru
// ===== Descrição: ===============================================
// = Vende todas as cartas dropadas por mobs, agrupadas em ordem alfabética.
// = Os cartões MVP são excluídos da lista.
// =
// = NOTA: Requer itens SQL e bancos de dados mob.
// ===== Changelogs: =============================================== =
// = 1.0 Primeira versão [AnnieRuru]
// = 1.1 Pequenas edições [Euphy]
// = 1.2 Atualização para o modo monstro e encanta [Lemongrass]
//============================================================

prontera,155,177,5	script	Vendedor de carta	100,{
	.@menu$ = getvariableofnpc( .alphabet_menu$, "card_seller_creation" );
	if (.@menu$ == "") {
		mes "[Vendedor de carta]";
		mes "Sinto muito, parece que algo deu errado.";
		mes "Não consigo encontrar nenhuma carta em nosso banco de dados no momento.";
		mes "Entre em contato com um GM.";
		close;
	}
	mes "[Vendedor de carta]";
	mes "Bem-vind"+(Sex?"o":"a")+"!";
	mes "Posso vender qualquer carta de monstro normal no jogo. Você gostaria de dar uma olhada?";
	next;
	.@s = select(.@menu$) -1;
	close2;
	callshop "card_mob#"+ getvariableofnpc( .alphabet$[.@s], "card_seller_creation" ), 1;
	end;
}

-	script	card_seller_creation	-1,{
	end;
OnInit:
	if (checkre(0)) {
		.@mob_db$  = "mob_db_re";
		.@item_db$ = "item_db_re";
	} else {
		.@mob_db$  = "mob_db";
		.@item_db$ = "item_db";
	}
	freeloop 1;	
	.@total = query_sql( "SELECT DISTINCT LEFT( `name_japanese`, 1 ) AS alphabets FROM `"+ .@item_db$ +"` WHERE `type` = " + IT_CARD + " AND `id` IN ( SELECT DISTINCT `dropcardid` FROM `"+ .@mob_db$ +"` WHERE ~(`MODE`) & " + MD_MVP + " ) ORDER BY alphabets;", .@alphabet$ );
	for ( .@i = 0; .@i < .@total; .@i++ ) {
		.@nb = query_sql( "SELECT `"+ .@item_db$ +"`.`id` FROM `"+ .@item_db$ +"` WHERE `type` = " + IT_CARD + " AND LEFT( `name_japanese`, 1 ) = '"+ .@alphabet$[.@i] +"' AND `id` IN ( SELECT DISTINCT `dropcardid` FROM `"+ .@mob_db$ +"` WHERE ~(`MODE`) & " + MD_MVP + " ) ORDER BY `name_japanese` LIMIT 128;", .@id );
		if (.@nb > 0) {
			.alphabet$[.@size_alphabet++] = .@alphabet$[.@i];
			.alphabet_menu$ = .alphabet_menu$ + .@alphabet$[.@i] +" Cartas:";
			npcshopdelitem "card_mob#"+ .@alphabet$[.@i], 501;
			for ( .@j = 0; .@j < .@nb; .@j++ ) {
				if (callfunc( "F_IsCharm", .@id[.@j] ) == true)// Ignorar encantamentos no caso de alguém os adicionar como entrega de cartas
					continue;
				npcshopadditem "card_mob#"+ .@alphabet$[.@i], .@id[.@j], 1000000;
			}
		}
	}
	freeloop 0;
	end;
}
-	shop	card_mob#A	-1,501:1000
-	shop	card_mob#B	-1,501:1000
-	shop	card_mob#C	-1,501:1000
-	shop	card_mob#D	-1,501:1000
-	shop	card_mob#E	-1,501:1000
-	shop	card_mob#F	-1,501:1000
-	shop	card_mob#G	-1,501:1000
-	shop	card_mob#H	-1,501:1000
-	shop	card_mob#I	-1,501:1000
-	shop	card_mob#J	-1,501:1000
-	shop	card_mob#K	-1,501:1000
-	shop	card_mob#L	-1,501:1000
-	shop	card_mob#M	-1,501:1000
-	shop	card_mob#N	-1,501:1000
-	shop	card_mob#O	-1,501:1000
-	shop	card_mob#P	-1,501:1000
-	shop	card_mob#Q	-1,501:1000
-	shop	card_mob#R	-1,501:1000
-	shop	card_mob#S	-1,501:1000
-	shop	card_mob#T	-1,501:1000
-	shop	card_mob#U	-1,501:1000
-	shop	card_mob#V	-1,501:1000
-	shop	card_mob#W	-1,501:1000
-	shop	card_mob#X	-1,501:1000
-	shop	card_mob#Y	-1,501:1000
-	shop	card_mob#Z	-1,501:1000
