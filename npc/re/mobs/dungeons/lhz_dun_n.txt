// ===== Script rAthena ===========================================
// = Nightmare Biolabs
// ===== Descrição: ===============================================
// = Script de Spawn de Monstros do Nightmare Biolabs.
// ===== Traduzido por: ===============================================
// = Guru
// ===== Comentários adicionais: ===================================
// = 1.0 Primeira versão. Boss e slaves aparecem como suposições
// com base no spawn de monstros lhz_dun.txt. [Capuche]
// = 1.1 Geração de MvP alterada com base na quantidade de
// monstros mortos. [Capuche]
//============================================================

lhz_dun_n,0,0	monster	Eremes Guille	3208,20,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Magaleta Sorin	3209,10,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Catherine Cheiron	3210,15,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Shecil Damon	3211,20,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Harword Alt-Eisen	3212,10,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Seyren Windsor	3213,10,5000,0,"lhz_dun_n::OnRegularDead"

lhz_dun_n,0,0	monster	Randel Lawrence	3226,10,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Flamel Emule	3227,10,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Celia Alde	3228,15,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Chen Liu	3229,15,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Gertie Wie	3230,15,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Alphoccio Basil	3231,10,5000,0,"lhz_dun_n::OnRegularDead"
lhz_dun_n,0,0	monster	Trentini	3232,10,5000,0,"lhz_dun_n::OnRegularDead"

lhz_dun_n,0,0,0	script	lhz_dun_n	-1,{
	end;
OnRegularDead:
	.@mob_id = killedrid;
	if (.lhz_dun_n[.@mob_id] < 110 && .lhz_dun_n_boss == 0) {
		.lhz_dun_n[.@mob_id]++;
		if (.lhz_dun_n[.@mob_id] == 110) {
			.@mini_boss = .@mob_id + 6 + (.@mob_id > 3213);	// ID da versão do mini boss: ID normal +6 para não transcendente, +7 para outros
			monster "lhz_dun_n",0,0,"--en--",.@mini_boss,1, "lhz_dun_n::OnMiniDead";
		}
	}
	end;
OnMiniDead:
	.@mob_id = killedrid;
	if (.lhz_dun_n[.@mob_id] < 4 && .lhz_dun_n_boss == 0) {
		.lhz_dun_n[.@mob_id]++;
		.@regular = .@mob_id - 6 - (.@mob_id > 3219);
		if (.lhz_dun_n[.@mob_id] < 4)
			.lhz_dun_n[.@regular] = 0;	// nota: a versão do mini boss pode surgir novamente quando a contagem de mini boss mortos é atingida? (atualmente eles não fazem)
		else {
			setarray .@mini_boss_list[0], 3214,3215,3216,3217,3218,3219,3233,3234,3235,3236,3237,3238,3239;
			for ( .@i = 0; .@i < 13; ++.@i ) {	
				if (.lhz_dun_n[.@mini_boss_list[.@i]] < 4)
					end;
			}
			// MvP monster seems to only spawn at 12 o'clock
			monster "lhz_dun_n",140,230,"--en--", F_Rand(3220,3221,3222,3223,3224,3225,3240,3241,3242,3243,3244,3245,3246),1, "lhz_dun_n::OnMyMVPDead";
			mapannounce "lhz_dun_n", "Voz do Ceifeiro: Já faz um tempo.", bc_map;	// mensagem desconhecida
			.lhz_dun_n_boss = 1;
			for ( .@i = 0; .@i < 13; ++.@i ) {	
				.@regular = .@mini_boss_list[.@i] - 6 - (.@mini_boss_list[.@i] > 3219);
				.lhz_dun_n[.@regular] = 0;
				.lhz_dun_n[.@mini_boss_list[.@i]] = 0;
			}
		}
	}
	end;
OnMyMVPDead:
	initnpctimer;
	.lhz_dun_n_delay = 0;
	killmonster "lhz_dun_n", "lhz_dun_n::OnMyMVPDead";
	mapannounce "lhz_dun_n", "Voz do Ceifeiro: Vejo você em breve.", bc_map, "0x00FF00";
	end;
OnTimer60000:
	.lhz_dun_n_delay++;
	if (.lhz_dun_n_delay < 100) // atraso desconhecido (atualmente 100 minutos)
		initnpctimer;
	else {
		stopnpctimer;
		.lhz_dun_n_delay = 0;
		.lhz_dun_n_boss = 0;
	}
	end;
}
