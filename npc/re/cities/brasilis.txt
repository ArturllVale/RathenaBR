// ===== Script rAthena ===========================================
// = Brasilis Town
// ===== Por: =============================================== =========
// = L0ne_W0lf
// ===== Traduzido Por: ===============================================
// = Guru
// ===== Versão Atual: ===========================================
// = 1,4
// ===== Compatível com: =========================================
// = Projeto rAthena
// ===== Descrição: ===============================================
// = [Conversão oficial]
// = Escrita da cidade de Brasilis
// ===== Comentários adicionais: ===================================
// = 1.0 Primeira versão. Transporte e NPCs básicos.
// = 1.1 Corrigido o Zeny não sendo removido para pagamento. [Kisuka]
// = 1.2 Diálogo atualizado, adicionado Sorveteiro. [L0ne_W0lf]
// = 1.3 Movido Alberta NPC para caminhos pre-re / re. [Euphy]
// = 1.4 Adicionados recursos VIP. [Euphy]
// ====================================================== ============

// Transporte Brasilis :: brasilis_trans
// ====================================================== ============
/* Coordenadas de pré-renovação: alberta, 247.115,3 */
alberta,246,82,3	script	Tripulante#bra2	100,{
	mes "[Tripulante]";
	mes "Ei, você já ouviu falar de um lugar chamado Brasilis?";
	mes "É uma cidade tropical quente como o deserto, mas também chuvosa. É um lugar muito misterioso.";
	next;
	mes "[Tripulante]";
	mes "Recentemente, encontramos uma nova rota marítima para chegar lá facilmente.";
	if (VIP_SCRIPT) {
		set .@cost, vip_status(VIP_STATUS_ACTIVE)?1000:10000;
		mes "Custa apenas 10.000 zeny para uma viagem de ida e volta e 1.000 para VIP! Então você quer ir?"; //tradução personalizada (VIP)
	} else {
		set .@cost,10000;
		mes "São apenas 10.000 zeny para uma viagem de ida e volta! Então você quer ir?";
	}
	next;
	switch(select("Leve-me para Brasilis!:Eu ficarei aqui.")) {
	case 1:
		if (Zeny < .@cost) {
			mes "[Tripulante]";
			mes "Eu disse "+callfunc("F_InsertComma",.@cost)+" zeny.";
			close;
		}
		else {
			mes "[Tripulante]";
			mes "Legal~ !! Vamos lá!";
			set Zeny, Zeny-.@cost;
			close2;
			warp "brasilis",314,60;
			end;
		}
	case 2:
		mes "[Tripulante]";
		mes "Bem, se você estiver interessado, me avise e eu posso te levar lá.";
		close;
	}
}

brasilis,316,57,3	script	Tripulante#bra1	100,{
	mes "[Tripulante]";
	mes "Meu navio vai voltar para Alberta, você quer se juntar a nós?";
	next;
	switch(select("Volte para Alberta:Ainda não")) {
	case 1:
		mes "[Tripulante]";
		mes "Tenho certeza que sinto falta de casa.";
		close2;
		if (checkre(0))
			warp "alberta",243,82;
		else
			warp "alberta",244,115;
		end;
	case 2:
		mes "[Tripulante]";
		mes "Ok, fique à vontade. Veremos você quando voltarmos.";
		close;
	}
}

// Generic Brasilis NPCs :: brazil_tre
//============================================================
brasilis,155,165,3	script	Placa de sinalização#bra1	858,{
	mes ":: Art Museum ::";
	close;
}

brasilis,191,239,3	script	Placa de sinalização#bra2	858,{
	mes ":: Verass Monument ::";
	close;
}

brasilis,240,247,3	script	Placa de sinalização#bra3	858,{
	mes ":: Market ::";
	mes " ";
	mes "- Para suas poções e armas -";
	close;
}

brasilis,303,309,3	script	Placa de sinalização#bra4	858,{
	mes ":: Jungle Cable ::";
	mes "- Não é para os fracos de coração -";
	close;
}

brasilis,278,137,3	script	Placa de sinalização#bra5	858,{
	mes ":: Brasilis Hotel ::";
	close;
}

brasilis,137,77,5	script	Sorveteiro	85,{
	mes "[Sorveteiro]";
	mes "Venha~ venha~";
	mes "Sorvete é o lanche perfeito para um dia quente~";
	mes "É apenas ^3355FF100 Zeny^000000~";
	mes "Sorvete~";
	mes "Pegue seu sorvete!";
	next;
	switch(select("Me dê um!:Sorvete?:Cancelar")) {
	case 1:
		mes "[Sorveteiro]";
		mes "Como há tantas pessoas que querem um sorvete legal, você pode pedir apenas 5 de cada vez.";
		mes "Então, quantos você quer?";
		next;
		while(1) {
			input .@input; //,1,5;
			if (.@input == 0) {
				mes "[Sorveteiro]";
				mes "Nenhum?";
				mes "Muito bem, saia da frente, tenho clientes para servir.";
				close;
			}
			else if ((.@input < 0) || (.@input > 5)) {
				mes "[Sorveteiro]";
				mes "Uau.";
				mes "Você pediu muito.";
				mes "Se você comer mais de 5, talvez precise lutar com um monstro em seu estômago. Fique calmo, camarada.";
				next;
			}
			else
				break;
		}
		set .@icecream_hap,.@input*100;
		if (Zeny < .@icecream_hap) {
			mes "[Sorveteiro]";
			mes "Iiii~! Você não tem dinheiro suficiente.";
			mes "É só ^3355FF100 Zeny^000000~ Seriamente!";
			close;
		}
		if (!checkweight(536,.@input)) {
			mes "[Sorveteiro]";
			mes "Você parece ter muitas coisas.";
			mes "Limpe sua mochila antes de comprar isso.";
			close;
		}
		set Zeny, Zeny-.@icecream_hap;
		getitem 536,.@input; //Sorvete
		close;
	case 2:
		mes "[Sorveteiro]";
		mes "'Sorvete é...";
		mes "Espere você não sabe";
		mes "o que é sorvete?";
		mes "de que rocha você vem";
		mes "tem vivido de'baixo? ";
		next;
		mes "[Sorveteiro]";
		mes "Não vou nem falar o quão estranho isso soa.";
		mes "Enfim, pegue seu sorvete aqui enquanto está bom e frio.";
		close;
	case 3:
		mes "[Sorveteiro]";
		mes "Não perca a chance de comer o maior sorvete de toda a terra~!";
		close;
	}
}
