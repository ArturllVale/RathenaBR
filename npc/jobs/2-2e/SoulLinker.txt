morocc_in,174,30,6	script	Garoto#link1	716,{
	if (Class == Job_Soul_Linker) {
		mes "[Maia]";
		mes "Boa sorte em sua jornada.";
		mes "Quando você dominar mais habilidades de Espiritualista, você será capaz de usar mais poderes dos Espíritos sobre os seus companheiros...";
		close;
	}
	if (Class == Job_Star_Gladiator) {
		mes "[Garoto]";
		if (Sex == SEX_MALE) {
			mes "Você é um guerreiro do sol? Eu estou familiarizado";
		}
		else {
			mes "Você é um guerreiro da lua? Eu estou familiarizado";
		}
		mes "com seus caminhos.";
		mes "Afinal, a base de nossas habilidades estão fundamentadas nas da classe Taekwon, certo?";
		close;
	}
	if (Class != Job_Taekwon) {
		mes "[Garoto]";
		mes "Hm... Eu não tenho nada a oferecer para você.";
		mes "Mas se você conhecer algum praticante de Taekwon Dô experiente, mande-os até mim, pelo que sei eles poderão se beneficiar com meus conhecimentos.";
		close;
	}
	if (JobLevel < 40) {
		mes "[Garoto]";
		mes "Então você está estudando Taekwon Dô.";
		mes "Isso é bom na verdade muito bom.";
		mes "Basta aprimorar sua habilidades e manter um bom treinamento.";
		close;
	}
	else if (JobLevel > 39) {
		if (SOUL_Q == 0) {
			mes "[Garoto]";
			mes "...";
			mes "Ei você.";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "Você está me chamando?";
			next;
			mes "[Garoto]";
			mes "Sim, eu chamei você.";
			mes "Agora não me faça falar muito alto, e venha até aqui.";
			next;
			if (select("Você está sendo muito rude para um garoto!:Ignorá-lo.") == 1) {
				mes "[Garoto]";
				mes "Você está com sorte, tenho algo interessante para você! Você pode me ver como uma criança, Mas eu tenho mais de 300 anos de idade!";
				emotion ET_HNG;
				next;
				mes "[Garoto]";
				mes "Agora ouça...";
				mes "Eu sei que você é um discípulo de Taekwon Dô.";
				mes "É uma arte respeitável, mas eu gostaria de lhe fazer uma proposta.";
				emotion ET_SMILE;
				next;
				mes "[Garoto]";
				mes "Eu estive olhando para você, e  posso dizer que você tem um grande poder espiritual.";
				mes "E esse seu potêncial não deve ser desperdiçado.";
				mes "Então porque não fazer de você um ''Espiritualista''?";
				next;
				if (select("Ha! garoto bobo.:Espiritualista?") == 1) {
					mes "[Garoto]";
					mes "Você... você não acredita em mim? Eu estou falando sério.";
					mes "Você pode pelo menos por um minuto esquecer o fato de que eu pareço uma criança? *Psh* ...Jovens.";
					close;
				}
				mes "[Garoto]";
				mes "Espiritualistas são capazes de comunicar-se com Espíritos de antigos guerreiros mortos, que ainda desejam lutar no mundo dos vivos.";
				mes "Agora, esses Espíritos guerreiros não podem lutar livremente em nosso mundo.";
				next;
				mes "[Garoto]";
				mes "No entanto, uma vez que eles são invocados, esses Espíritos são atraídos para você.";
				mes "Com o treinamento correto, você pode temporariamente atribuir o poder desses Espíritos aos seus aliados.";
				next;
				mes "[Garoto]";
				mes "Agora, você pode atribuir também o poder desses Espíritos a si mesmo.";
				mes "Dependendo, de suas habilidades de Espiritualista, você poderá apenas atribuir o poder dos Espíritos para algumas classes.";
				next;
				mes "[Garoto]";
				mes "Você vai ter de entrar em um mundo totalmente diferente para se tornar um Espiritualista, mas eu sei que isso vai ser possível para você. E então o que me diz?";
				next;
				if (select("Não. Pelo menos, não agora...:Tudo bem. O que eu tenho que fazer?") == 1) {
					mes "[Garoto]";
					mes "Ah, Tudo bem. Mas, se você decidir tornar-se um Espiritualista, volte até aqui e fale comigo a qualquer momento.";
					close;
				}
				set SOUL_Q,1;
				setquest 6005;
				mes "[Garoto]";
				mes "Então você está decidido a tornar-se um Espiritualista? Muito bom! Tudo bem, primeiro você precisa me trazer alguns itens.";
				mes "Não se preocupe, eu vou explicar por que você precisa deles, mais tarde.";
				next;
				mes "[Garoto]";
				mes "Agora traga-me";
				mes "^0000FF1 Diamante de 3 Quilates^000000,";
				mes "^0000FF1 Coração Imortal^000000 e";
				mes "^0000FF1 Rosa Eterna^000000.";
				mes "E não me faça esperar por muito tempo, ok?";
				close;
			}
			mes "[Garoto]";
			mes "Huh...?";
			mes "Espere, para onde você vai? Eu...";
			mes "Eu estou falando com você!";
			close;
		}
		else if (SOUL_Q == 1) {
			if (Class == Job_Taekwon) {
				mes "[Garoto]";
				mes "Você voltou, em?";
				mes "E então você trouxe";
				mes "^0000FF1 Diamante de 3 Quilates^000000,";
				mes "^0000FF1 Coração Imortal^000000 e";
				mes "^0000FF1 Rosa Eterna^000000.";
				mes "assim como eu pedi?";
				next;
				if (select("Sim, eu trouxe.:Não, ainda não...") == 1) {
					if (countitem(732) > 0 && countitem(929) > 0 && countitem(748) > 0) {
						delitem 732,1; // Diamante de 3 Quilates
						delitem 929,1; // Coração Imortal
						delitem 748,1; // Rosa Eterna
						set SOUL_Q,2;
						changequest 6005,6006;
						mes "[Garoto]";
						mes "Ótimo, eu vejo que você me trouxe tudo. Mas antes de começar, deixe que eu me apresente. Meu nome é Maia, e eu estou vivo por mais de 300 anos.";
						next;
						mes "[Maia]";
						mes "Bem mas pulando os detalhes, é o meu dever encontrar e recrutar mais Espiritualistas. Essa é a razão pela qual eu não deixei, você sabe, passar direto.";
						next;
						mes "[Maia]";
						mes "De toda forma, eu preciso terminar os preparativos com os materiais que você me trouxe, então preciso que você saia e volte depois, ok?";
						mes "E então, nos falaremos novamente.";
						close;
					}
					mes "[Garoto]";
					mes "Hm...?";
					mes "Ei, você esqueceu de alguma coisa.";
					mes "Agora vá e me traga tudo que eu havia te pedido, ok?";
					emotion ET_HNG;
					next;
					mes "[Garoto]";
					mes "Eu sei que você sabe o que precisa trazer, mas eu vou repetir novamente:";
					mes "^0000FF1 Diamanete de 3 Quilates^000000,";
					mes "^0000FF1 Coração Imortal^000000 e";
					mes "^0000FF1 Rosa Eterna^000000.";
					close;
				}
				mes "[Garoto]";
				mes "Hm. Mas isso é bom.";
				mes "Embora eu tenha todo tempo do mundo, eu não gosto de esperar muito.";
				close;
			}
			set SOUL_Q,0;
			mes "[Garoto]";
			mes "Você se tornou um guerreiro do Sol, da Lua e das Estrelas em? Eu não tinha ideia de que você tinha todo esse potencial.";
			mes "Acho que não posso culpá-lo...";
			close;
		}
		else if (SOUL_Q == 2) {
			if (SkillPoint) {
				mes "[Maia]";
				mes "Você ainda possui alguns pontos não distribuidos.";
				mes "Use todos eles para aprender as habilidades de Taekwon Dô, e depois volte quando estiver pronto.";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[Maia]";
				mes "Certo agora, preciso terminar a cerimônia que vai transformá-lo em Espiritualista.";
				mes "Então você poderia esperar até que eu termine? Então, quando estiver pronto, eu irei atendê-lo.";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[Maia]";
			mes "Ótimo, eu terminei os preparativos. Agora está pronta a cerimônia de mudança para você tornar-se um Espiritualista.";
			mes "Agora feche seus olhos...";
			close2;
			warp "job_soul",30,30;
			end;
		}
		else if (SOUL_Q > 2) {
			mes "[Maia]";
			mes "Você está pronto para entrar nas profudenzas da sua mente de novo?";
			next;
			if (select("Não:Sim") == 1) {
				mes "[Maia]";
				mes "Então, volte a falar comigo quando você estiver pronto.";
				mes "Até lá, eu estarei esperando bem aqui.";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[Maia]";
				mes "Certo agora, preciso terminar a cerimônia que vai transformá-lo em Espiritualista.";
				mes "Então você poderia esperar até que eu termine? Então, quando estiver pronto, eu irei atendê-lo.";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[Maia]";
			mes "Tudo bem, feche seus olhos e relaxe.";
			mes "Você será levado para as profundezas de sua mente.";
			close2;
			warp "job_soul",30,30;
			end;
		}
	}

OnInit:
	set .SoulLinkerTest,0;
	end;
}

job_soul,30,31,0	script	Maia#link2::SLTester	-1,3,3,{
OnTouch_:
	if (Class == Job_Taekwon) {
		if (JobLevel < 40) {
			set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
			mes "[Maia]";
			mes "Hm? Como você veio parar aqui? Você não está qualificado para esta cerimônia. Vou levá-lo de volta para Morroc...";
			close2;
			warp "morocc",157,47;
			end;
		}
		if (SOUL_Q == 2) {
			mes "[Maia]";
			mes "Você reconhece esse lugar? Certo, agora estamos dentro de sua mente.";
			mes "Os Espíritos dos guerreiros que morreram estão aqui, esperando que você invoque seus poderes.";
			next;
			mes "[Maia]";
			mes "Certo, agora há apenas alguns deles aqui, mas se você continuar treinando, você";
			mes "será capaz de invocar mais Espíritos como um Espiritualista.";
			next;
			set SOUL_Q,3;
			changequest 6006,6007;
			mes "[Maia]";
			mes "Nós só podemos ficar aqui na sua mente por 3 minutos.";
			mes "Eu sugiro que você fale com os Espíritos enquanto tem a oportunidade.";
			close;
		}
		else if (SOUL_Q == 3) {
			mes "[Maia]";
			mes "Ouça o que os Espíritos estão tentando te dizer.";
			mes "Há uma razão pela qual eles não podem seguir em frente para o próximo mundo.";
			close;
		}
		else if (SOUL_Q == 4) {
			mes "[Maia]";
			mes "Eu acredito que você já está pronto para tornar-se um Espiritualista.";
			mes "No entanto, você pode continuar a falar com os Espíritos se for isso que você deseja.";
			next;
			if (select("Conversar com os espíritos:Tornar-se um Espiritualista") == 1) {
				mes "[Maia]";
				mes "Tudo bem. Mas tente ser rápido, uma vez que há um limite de tempo para permanecer em sua mente.";
				mes "Embora, nós possamos voltar à sua mente se você falar comigo mais tarde...";
				close;
			}
			if (ismounting()) {
				mes "[Maia]";
				mes "Você está em uma montaria, então você não pode mudar de classe.";
				mes "Por favor, remova sua montaria e tente novamente!";
				close;
			}
			mes "[Maia]";
			mes "Então vamos começar a cerimônia.";
			mes "Esses itens serão usados para você ganhar a capacidade de conjurar os poderes dos Espíritos antigos em seus companheiros no campo de batalha.";
			next;
			mes "[Maia]";
			mes "Esta rosa eterna vai morrer ao invés de você...";
			specialeffect EF_MAPPILLAR2,AREA,"Maia#link2";
			next;
			mes "[Maia]";
			mes "Esta rosa eterna vai morrer ao invés de você...";
			mes "Este coração imortal irá bombear o sangue ao invés do seu. ";
			next;
			mes "[Maia]";
			mes "Esta rosa eterna vai morrer ao invés de você...";
			mes "Este coração imortal irá bombear o sangue ao invés do seu.";
			mes "Este diamante irá se tornar pó, ao invés do seu corpo mortal.";
			next;
			mes "[Maia]";
			mes "Os mortos que desejarem";
			mes "continuarão a lutar...";
			mes "Lutarão com você! Use seus poderes de Espiritualista com sabedoria e para fins justos.";
			next;
			if (SkillPoint) {
				mes "^0000ffVocê ainda tem pontos de habilidade sobrando. Por favor, use todos os pontos de habilidades restantes e tente novamente!^000000";
				close;
			}
			completequest 6008;
			callfunc "Job_Change",Job_Soul_Linker;
			callfunc "F_ClearJobVar";
			set SOUL_Q,0;
			mes "[Maia]";
			mes "Desejo a você boa sorte em sua nova vida.";
			mes "Permaneça sempre ao lado de seus aliados, e os espirítos irão te proteger e te ajudar a lutar em suas batalhas. Adeus por agora, amigo.";
			close2;
			set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
			donpcevent "Timer#link3::OnDisable";
			warp "morocc",157,47;
			end;
		}
		set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
		mes "[Maia]";
		mes "Hmm...?";
		mes "Parece que o seu tempo de ficar aqui acabou.";
		mes "Vamos voltar para Morroc...";
		close2;
		warp "morocc",157,47;
		end;
	}
	set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
	if (Class == Job_Soul_Linker) {
		mes "[Maia]";
		mes "Chegou a hora de você seguir seu rumo no mundo! Mais Espiritualistas definitivamente serão necessários nas batalhas contra o mal...";
	}
	else {
		mes "[Maia]";
		mes "Isto é estranho...";
		mes "você não deveria estar aqui.";
		mes "Deixe-me te levar de volta para Morroc...";
	}
	close2;
	warp "morocc",157,47;
	end;
}

job_soul,35,30,6	duplicate(SLTester)	Maia#link6	4_M_KID2

job_soul,30,35,6	script	Espírito do Monge#link4	4_M_GRANDMONK,{
	if (SOUL_Q == 2) {
		mes "[Espírito do Monge]";
		mes "Quem eu sou...?";
		mes "Eu acho...";	
		mes "Eu acho melhor você falar com o Maia primeiro...";
		mes "Dizer quem eu sou requer uma explicação complicada...";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Espírito do Monge]";
		mes "Durante minha vida, eu e meus colegas fazíamos o melhor para garantir que faríamos tudo que pudessemos como um Monge.";
		mes "Mesmo assim...";
		mes "Eu ainda não acho que fiz o melhor uso de minhas habilidades.";
		next;
		mes "[Espírito do Monge]";
		mes "Na morte, eu tive muitos arrependimentos, nunca tive a chance de ensinar minhas habilidades para os novos Monges.";
		mes "Emprestar meu poder para os outros é a única chance que tenho de tornar isso possível.";
		next;
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		mes "[Espírito do Monge]";
		mes "Eu imploro...";
		mes "Eu preciso de sua ajuda para mostrar a todos";
		mes "meu verdadeiro potencial para os Monges de hoje.";
		close;
	}
	mes "[Espírito do Monge]";
	mes "...";
	close;
}

job_soul,30,25,7	script	Espírito do Sábio#link5	4_M_SAGE_A,{
	if (SOUL_Q == 2) {
		mes "[Espírito do Sábio]";
		mes "Fale com Maia.";
		mes "Tenho medo que eu possa confundir você se você não ouvir primeiro as explicações de Maia...";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Espírito do Sábio]";
		mes "Minha busca pelo conhecimento concedeu-me um poder incrível.";
		mes "Na vida, eu poderia destruir qualquer coisa que quisese.";
		mes "Poucos Sábios conseguiriam chegar ao meu nível...";
		next;
		mes "[Espírito do Sábio]";
		mes "Eu morri, mas eu não sou capaz de passar para o outro mundo.";
		mes "Eu ainda quero usar minhas habilidades.";
		mes "Eu quero usar meu conhecimento para construir o que eu quiser, e destruir o que for preciso.";
		next;
		mes "[Espírito do Sábio]";
		mes "Para mim é suficiente saber que eu posso dar meu poder a um Sábio que seja digno de recebê-lo.";
		mes "Mas para isso, Eu preciso de sua ajuda.";
		mes "Eu imploro, deixe-me ser seu espirito aliado.";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[Espírito do Sábio]";
		mes "Eu acredito que você é o único que pode trazer descanso à minha alma perturbada...";
		close;
	}
	mes "[Espírito do Sábio]";
	mes "...";
	close;
}

job_soul,25,30,5	script	Espírito do Alquimista#link7	4_F_ALCHE,{
	if (SOUL_Q == 2) {
		mes "[Espírito do Alquimista]";
		mes "Oh! Eu realmente quero falar com você, mas o que eu tenho a dizer não fará muito sentido a menos que você fale com Maia primeiro.";
		mes "Mas sim, eu realmente preciso de sua ajuda.";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Espírito do Alquimista]";
		mes "Sem exageros, eu era o mais rápido Alquimista da minha época.";
		mes "Na verdade, eu ainda posso ser o Alquimista mais rápido.";
		mes "Mas por isso eu era arrogante, e acabei me matando em um acidente.";
		next;
		mes "[Espírito do Alquimista]";
		mes "Mas a morte não apagou minhas habilidades.";
		mes "Na verdade, eu ainda melhorei minhas habilidades desde que morri.";
		mes "Mas eu não posso ir para o outro mundo, não antes de re-passar minhas tecnicas...";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[Espírito do Alquimista]";
		mes "Eu sou impotente como um espirito, mas se tiver sua ajuda, eu posso ser uma influência para os Alquimistas de hoje e ajudá-los a melhorar as suas habilidades. Eu lhe peço, por favor me dê esta chance...";
		close;
	}
	mes "[Espírito do Alquimista]";
	mes "...";
	close;
}

job_soul,1,5,0	script	Timer#link3	HIDDEN_NPC,{
	end;

OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
	end;

OnTimer60000:
OnTimer120000:
	if (getmapusers("job_soul") == 0) {
		stopnpctimer;
		set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
	}
	end;

OnTimer180000:
OnTimer181000:
OnTimer182000:
	mapwarp "job_soul","morocc",157,47;
	end;

OnTimer183000:
	mapwarp "job_soul","morocc",157,47;
	set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
	stopnpctimer;
}

sec_in02,35,153,0	script	Variáveis Espiritualista	4_M_OPERATION,{
	callfunc "F_GM_NPC";
	mes "[Variáveis Espiritualista]";
	mes "Eu posso resetar os NPCs da mudança de classe para Espiritualista se um candidato encontrar problemas durante o final da quest.";
	mes "Por favor, não utilize essa função se ainda existirem jogadores no mapa.";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Variáveis Espiritualista]";
		mes "Senha";
		mes "incorreta.";
		close;
	} else {
		mes "[Variáveis Espiritualista]";
		mes "Você gostaria de resetar a Variável Global da Mudança de Classe para Espiritualista?";
		next;
		switch(select("Resetar:Cancelar")) {
		case 1:
			mes "[Variáveis Espiritualista]";
			mes "Os NPCs da mudança de classe para Espiritualista foram resetados.";
			set getvariableofnpc(.SoulLinkerTest,"Garoto#link1"),0;
			close;
		case 2:
			mes "[Variáveis Espiritualista]";
			mes "Você cancelou este comando.";
			close;
		}
	}
}