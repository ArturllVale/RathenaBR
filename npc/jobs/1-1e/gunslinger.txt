// ===== Script rAthena ===========================================
// = Gunslinger Job Quest
// ===== Por: =============================================== =========
// = erKURITA, RockmanEXE, Kisuka
// ===== Traduzido Por: =============================================== 
// = Guru
// ===== Versão Atual: ===========================================
// = 2,2
// ===== Compatível com: =========================================
// = Projeto rAthena
// ===== Descrição: ===============================================
// = Busca de mudança de emprego de pistoleiro
// ===== Comentários adicionais: ===================================
// = 1.0 Feito o NPC [erKURITA]
// = 1.5 Acoplar correções ao npc, bem como adicionar o que falta
// = 3 ervas verdes. [erKURITA]
// = 1.6 número do trabalho-> const, nome da variável comum,
// = otimizado [Lupus]
// = 1.7 Agora usa apenas UMA variável GUNS_Q [Lupus]
// = 1.7a Correções de gramática, poucas correções de ortografia, etc [CBMaster]
// = 1.7b Correções de parênteses [KarLaeda]
// = 1.8 Corrigida a recompensa que você pode obter [Playtester]
// = 1.9 Removidas algumas construções de script 'inteligentes' [ultramage]
// = 2.0 Atualizado para a versão oficial - Agradecimentos à Omega por
// = sua primeira tentativa [SinSloth] 1.10b - removido .GATs [Lupus]
// = 2.1 Sábio Chifre de Touro agora exclui todos os itens. Graças a yyCC. [L0ne_W0lf]
// = 2.1a Corrigido um erro de digitação ";;". [Samuray22]
// = 2.1b Corrigidos alguns erros de digitação. (relatório de bug: 1549) [Samuray22]
// = 2.1c Corrigida algumas verificações de itens ausentes / confusos. (relatório de bug: 2693) [Kisuka]
// = 2.2 Atualizado para corresponder ao script AEGIS e aos comandos Quest Log adicionados. [Kisuka]
//============================================================ 

que_ng,152,167,3	script	Mestre Miller	901,{
	if (Upper == 2) {
		mes "[Mestre Miller]";
		mes "Bem, você não é uma";
		mes "adorável criancinha?~";
		mes "Onde está sua mamãe?";
		mes "Esse lugar é perigoso, então";
		mes "por favor, vá logo para casa, ok?";
		close;
	}
	if (Class == Job_Novice) {
		if (!callfunc("F_CanChangeJob")) {
			mes "[Mestre Miller]";
			mes "Interessado em se tornar";
			mes "um Justiceiros, hein? Você tem";
			mes "tem potencial, mas você";
			mes "ainda não experimentei o suficiente.";
			mes "Treine um pouco mais,";
			mes "e depois volte, ouviu?";
			close;
		}
		if (GUNS_Q == 0) {
			mes "[Mestre Miller]";
			mes "Eu sou Miller, um treinador de Justiceiros e guardião em tempo integral de Lady Selena.";
			mes "Pois bem, do que você precisa?";
			mes "Se não for importante, então não tenho tempo a perder com você.";
			next;
			if(select("Nada.:Eu quero me tornar um Justiceiro.") == 1) {
				mes "[Mestre Miller]";
				mes "Não disperdice meu tempo.";
				mes "Se você quiser se tornar um Justiceiro,";
				mes "volte e fale comigo";
				close;
			}
			mes "[Mestre Miller]";
			mes "Hm. você é muito jovem, mas seus olhos dizem que você é bastante ambicioso.";
			mes "você terá que passar em nossa entrevista e curso educacional para se tornar um Justiceiro.";
			mes "você quer se candidatar ao cargo?";
			next;
			if(select("Me de algum tempo para pensar.:Certo!") == 1) {
				mes "[Mestre Miller]";
				mes "Compreensível.";
				mes "Se você decidir que quer se tornar";
				mes "um Justiceiro, então me conte imediatamente.";
				mes "Eu te ajudarei a começar.";
				close;
			}
			mes "[Mestre Miller]";
			mes "Ótimo, ótimo.";
			mes "Muito bem, então vamos começar.";
			mes "Leve esta carta para o Sr. Sábio";
			mes "Chifre de Touro, em Payon.";
			mes "Ele é um xama que irá julgar se você é ou";
			mes "não qualificado para se tornar um Justiceiro.";
			set GUNS_Q,1;
			setquest 6020;
			close;
		}
		else if (GUNS_Q == 1) {
			mes "[Mestre Miller]";
			mes "Leve essa carta de apresentaçao";
			mes "que escrevi para o Sr. Sábio";
			mes "Chifre de Touro, em Payon.";
			mes "Ele te testará para ver se você pode se tornar um Justiceiro.";
			close;
		}
		else if (GUNS_Q == 2) {
			mes "[Mestre Miller]";
			mes "Hmm... o Sábio Chifre de Touro te pediu";
			mes "para coletar os itens que precisa para fazer o amuleto?";
			mes "Hm. Eu acho que isso faz parte do teste.";
			close;
		}
		else if (GUNS_Q == 3) {
			mes "[Mestre Miller]";
			mes "O Sábio Chifre de Touro te pediu um pouco de leite?";
			mes "Ele deve gostar de você, já que está pedindo favores.";
			mes "Boa sorte, amigo.";
			close;
		}
		else if (GUNS_Q == 4) {
			mes "[Mestre Miller]";
			mes "Eu espero ouvir boas notícias de você em breve.";
			mes "você sabe, eu não tenho dúvida";
			mes "que você irá se tornar um Justiceiro.";
			close;
		}
		else if (GUNS_Q == 5) {
			if (SkillPoint != 0) {
				mes "[Mestre Miller]";
				mes "Hey, você tem Pontos de Habilidades sobrando.";
				mes "Use-os antes de vir falar comigo de novo.";
				close;
			}
			mes "[Mestre Miller]";
			mes "Oh, você me trouxe um amuleto";
			mes "do Sábio Chifre de Touro?";
			mes "Já tem um tempo que ele não dá um pra ninguém...";
			mes "Estou muito orgulhoso de você!";
			next;
			mes "[Mestre Miller]";
			mes "Se o Sábio Chifre de Touro aprova, então";
			mes "eu não tenho motivos para rejeitar você.";
			mes "Tudo bem então, vou promove-lo a Justiceiro.";
			mes "Mas primeiro, deixe-me explicar sobre nossa";
			mes "classe mais detalhadamente.";
			next;
			mes "[Mestre Miller]";
			mes "Como um Justiceiro, você deve";
			mes "manter sua arma com você o tempo todo.";
			mes "A Guilda dos Justiceiros mantém o controle de";
			mes "todas as Armas e Projéteis, então você só pode obte-las dos membros da nossa guilda.";
			next;
			mes "[Mestre Miller]";
			mes "Não se preocupe, os membros da Guilda";
			mes "dos Justiceiros podem ser encontrados";
			mes "em quase todos os lugares.";
			mes "Enfim, tem que ser desta maneira,";
			mes "por ordem da nossa líder da guilda, Lady Selena.";
			next;
			mes "[Mestre Miller]";
			mes "você pode ter a chance de";
			mes "conhece-la um dia desses.";
			mes "De qualquer forma, agora temos que controlar";
			mes "a venda de armas e projéteis para mante-los longe";
			mes "de pessoas más ou irresponsáveis.";
			next;
			mes "[Mestre Miller]";
			mes "De qualquer forma, para mim é";
			mes "sempre um prazer conversar com";
			mes "outro Justiceiro, então mantenha contato.";
			mes "Que o poder da Terra te proteja em todas as suas aventuras~";
			callfunc "Job_Change",Job_Gunslinger;
			set GUNS_Q,6;
			completequest 6024;
			if (rand(1,2) == 1) {
				getitem 13100,1; // pistola de seis tiros
			} else {
				getitem 13150,1; // Branch
			}
			close;
		}
	} else {
		if (Class == Job_Gunslinger) {
			mes "[Mestre Miller]";
			mes "Oh! A quanto tempo não te vejo,";
			mes "amigo. Como vai? Espero que";
			mes "você tenha mantido sua arma bem cuidada.";
			mes "Cuide dela, e ela cuidará de você. Lembre-se disso.";
			close;
		} else {
			mes "[Mestre Miller]";
			mes "Se você não tem nenhum";
			mes "assunto a tratar comigo,";
			mes "então, por favor, siga seu caminho.";
			close;
		}
	}
}

payon,184,65,3	script	Sábio Chifre de Touro	866,{
	if (GUNS_Q == 1) {
		mes "[Sábio Chifre de Touro]";
		mes "Olá, jovem lobo.";
		mes "Que assuntos te trazem";
		mes "até minha presença?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "O Sr. Miller me mandou";
		mes "entregar esta carta a você.";
		mes "Na verdade, estou interessado em";
		mes "me tornar um Justiceiro...";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Miller, você disse?";
		mes "Hm, o Raposa Negra não costuma";
		mes "escrever apresentaçoes para qualquer";
		mes "um que ele não acredita que será um bom Justiceiro";
		mes "Sim, eu acho que sei por que ele o mandou a mim.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Posso ver em seus olhos";
		mes "que tem um bom coraçao e um";
		mes "forte senso de responsabilidade.";
		mes "Tudo o que precisa é da bençao da";
		mes "Terra para te proteger como Justiceiro.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eu posso fazer um amuleto que";
		mes "demonstrará seu desejo em se tornar";
		mes "um guerreiro da Terra, para que o apresente";
		mes "ao Mestre dos Justiceiros. Vou precisar destes itens...";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "^3355FF1 Tronco^000000,";
		mes "^3355FF3 Felpas^000000,";
		mes "^3355FF3 Zargônios^000000,";
		mes "^3355FF10 Cascas^000000,";
		mes "^3355FF3 Ervas Verdes^000000,";
		mes "e ^3355FF3 Cascos Arco-Íris^000000.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Depois que eu terminar o amuleto,";
		mes "você deve levá-lo ao Raposa Negra,";
		mes "e ele irá o ajudar a alcançar seu";
		mes "objetivo de se tornar um Justiceiro.";
		set GUNS_Q,2;
		changequest 6020,6021;
		close;
	}
	else if (GUNS_Q == 2) {
		if (countitem(912) < 3 || countitem(914) < 3 || countitem(1019) < 1 || countitem(935) < 10 || countitem(511) < 3 || countitem(1013) < 3) {
			mes "[Sábio Chifre de Touro]";
			mes "Eu posso fazer um amuleto que";
			mes "demonstrará seu desejo em se tornar";
			mes "um guerreiro da Terra, para que o";
			mes "apresente ao Mestre dos Justiceiros.";
			mes "Vou precisar destes itens...";
			next;
			mes "[Sábio Chifre de Touro]";
			mes "[Sábio Chifre de Touro]";
			mes "^3355FF1 Tronco^000000,";
			mes "^3355FF3 Felpas^000000,";
			mes "^3355FF3 Zargônios^000000,";
			mes "^3355FF10 Cascas^000000,";
			mes "^3355FF3 Ervas Verdes^000000,";
			mes "e ^3355FF3 Cascos Arco-Íris^000000.";
			next;
			mes "[Sábio Chifre de Touro]";
			mes "Depois que eu terminar o amuleto,";
			mes "você deve levá-lo ao Raposa Negra,";
			mes "e ele irá o ajudar a alcançar seu";
			mes "objetivo de se tornar um Justiceiro.";
			close;
		}
		delitem 912,3; // Zargônio
		delitem 914,3; // Felpa
		delitem 1019,1; // Tronco 
		delitem 935,10; // Casca 
		delitem 511,3; // Erva Verde
		delitem 1013,3; // Casco Arco-Íris 
		set GUNS_Q,3;
		changequest 6021,6022;
		mes "[Sábio Chifre de Touro]";
		mes "Ah, você voltou com tudo que eu preciso.";
		mes "Por favor, me de algum tempo para fazer o amuleto.";
		mes "Se você voltar daqui a pouco, eu devo ter terminado.";
		close;
	}
	else if (GUNS_Q == 3) {
		mes "[Sábio Chifre de Touro]";
		mes "Oh, você chegou na hora.";
		mes "Já faz um tempo que não faço um desses amuletos, então devo estar um pouco enferrujado.";
		mes "Ainda assim, isso realmente me leva de volta para os dias de minha juventude.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eu estive servindo nesta posiçao";
		mes "de escolher pessoas dignas do amuleto";
		mes "do Justiceiro por algumas décadas.";
		mes "Mas antes disso, eu era um jovem aventureiro, como você.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Parece que foi ontem que eu";
		mes "ganhei meu próprio amuleto do Justiceiro,";
		mes "um guerreiro da Terra.";
		mes "Foi quando eu conheci o pai de Selena.";
		mes "Como o tempo pode passar tao rápido?";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Ah...";
		mes "Eu realmente aprecio toda a ajuda de Selena e do Raposa Negra em ajuda em recrutar jovens Justiceiros.";
		mes "Eu estou bastante velho agora, e não consigo fazer tudo sozinho.";
		mes "*Suspiro...*";
		mes "Assim é a vida.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Antes de você ir, posso pedir um pequeno favor?";
		mes "Estou com sede, e eu gostaria de um copo de leite gelado.";
		mes "você poderia me trazer algum?";
		set GUNS_Q,4;
		changequest 6022,6023;
		close;
	}
	else if (GUNS_Q == 4) {
		if (countitem(519) < 1) {
			mes "[Sábio Chifre de Touro]";
			mes "Eu sou um velho homem que,";
			mes "em breve, se reunirá com a Mae Terra.";
			mes "você faria um favor a este velho Justiceiro";
			mes "de me trazer um copo de ^3131FFleite^000000 gelado, por favor?";
			close;
		}
		delitem 519,1; // Leite
		set GUNS_Q,5;
		changequest 6023,6024;
		mes "[Sábio Chifre de Touro]";
		mes "Oh, obrigado por sua generosidade!";
		mes "Eu vejo que você me trouxe um pouco de leite.";
		mes "Ahhhh, delícia...";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eu admiro a paciencia, gentileza,";
		mes "e bondade que você provou trazendo isto para mim.";
		mes "Sim, essas sao características que todos nós queremos ter.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Agora, por favor, leve esse";
		mes "amuleto para Miller, o Raposa Negra,";
		mes "com toda minha aprovaçao.";
		mes "Eu espero que você use sua arma para apoiar";
		mes "a justiça como um nobre guerreiro da Terra.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Maaaaarrraaa Neeey~";
		mes "Yippee Yippee Yai Yocaiyay~";
		close;
	}
	else if (GUNS_Q == 5) {
		mes "[Sábio Chifre de Touro]";
		mes "Por favor, leve esse amuleto para Miller,";
		mes "o Raposa Negra, com toda a minha aprovaçao.";
		mes "Eu espero que você use sua arma para apoiar";
		mes "a justiça como um nobre guerreiro da Terra.";
		next;
		mes "[Sábio Chifre de Touro]";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Eeh~Yeah~Eeh~Hooom";
		mes "Maaaaarrraaa Neeey~";
		mes "Yippee Yippee Yai Yocaiyay~";
		close;
	}
	else if (GUNS_Q == 6) {
		mes "[Sábio Chifre de Touro]";
		mes "Faz muito tempo que não te vejo.";
		mes "Espero que tenha se tornado uma fera";
		mes "inteligente, que usa seus poderes como";
		mes "Justiceiro para protecer o que é bom e justo.";
		close;
	}
	else {
		mes "[Sábio Chifre de Touro]";
		mes "Zzzzzz~";
		mes "^333333*Ufa*^000000";
		close;
	}
}
