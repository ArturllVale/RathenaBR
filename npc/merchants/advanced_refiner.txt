//===== rAthena Script =======================================
//= Advanced Refiner
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses Enriched ores to increase upgrade success.
//= - Dialog is only partly official to iRO.
//= - Uses the iRO position for this NPC.
//===== Changelog: ===========================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Fixed a weird carriage return. o_o [L0ne_W0lf]
//= 1.2 Optimizing refine method [Zephyrus]
//= 1.3 Typo fixes [Yommy]
//= 1.4 Removed unnecessary dialogs [Zephyrus]
//= 1.4a Added 'disable_items' command. [Euphy]
//= 1.4b Fixed coordinates. [Euphy]
//= 1.5 Some official script updates. [Euphy]
//= 1.6 Added VIP features. [Euphy]
//= 1.7 Removed re-roll behavior. [Secret]
//============================================================

payon,157,146,6	script	Suhnbi#cash	85,{
	disable_items;
	mes "[Suhnbi]";
	mes "Eu sou o Armsmith";
	mes "Eu posso refinar todos os tipos de armas,";
	mes "armadura e equipamento, então deixe-me";
	mes "saiber o que você deseja refinar.";
	next;

	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@indices[.@i])) {
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			set .@equipped,1;
		}
		set .@menu$, .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "[Suhnbi]";
		mes "Eu não acho que posso refinar os itens que você possui ...";
		close;
	}
	set .@part, .@indices[ select(.@menu$) ];

	if (!getequipisequiped(.@part)) //custom check
		close;
	if (!getequipisenableref(.@part)) {
		mes "[Suhnbi]";
		mes "Vá encontrar outro ferreiro. Você não pode refinar isso.";
		close;
	}
	if (getequiprefinerycnt(.@part) >= 10) {
		mes "[Suhnbi]";
		mes "Hmm ... alguém já aperfeiçoou isso. Eu não acho que posso trabalhar mais nisso.";
		close;
	}

	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	.@price = getequiprefinecost(.@part, REFINE_COST_ENRICHED, REFINE_ZENY_COST);
	.@material = getequiprefinecost(.@part, REFINE_COST_ENRICHED, REFINE_MATERIAL_ID);

	// Make sure you have the necessary items and Zeny to refine your items
	// Determines chance of failure and verifies that you want to continue.
	callsub S_RefineValidate,getequipweaponlv(.@part),.@material,.@price,.@part,.@refineitemid,.@refinerycnt;

	mes "[Suhnbi]";
	mes "Clang! Clang! Clang!";
	if (getequippercentrefinery(.@part, true) > rand(100)) {
		successrefitem .@part;
		next;
		emotion ET_BEST;
		mes "[Suhnbi]";
		mes "Ai está! Pronto.";
		mes "Já faz um tempo que não fiz uma"+((getequiparmalv(.@part))?"arma":"armadura")+". Você deve estar feliz porque ficou mais forte!";
		close;
	}
	failedrefitem .@part;
	next;
	emotion (!rand(5))?ET_MONEY:ET_HUK;
	mes "[Suhnbi]";
	mes "Uuuuuuuuuummmmmph !!!";
	next;
	mes "[Suhnbi]";
	mes "...";
	mes ".....";
	mes "....... Huhuhuhuhu ~";
	mes "........ Foi sua escolha e minha habilidade, sem arrependimento.";
	close;

S_RefineValidate:
	.@weapon_lvl = getarg(0);
	.@item_req = getarg(1);
	.@price = getarg(2);
	.@part = getarg(3);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);

	// If the VIP system is enabled, the prices for non-VIP players are considerably higher.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		switch(.@weapon_lvl){
			case 0: set .@price, .@price * 10; break;
			case 1: set .@price, .@price * 40; break;
			case 2: set .@price, .@price * 50; break;
			case 3: set .@price, .@price * 2; break;
			case 4: set .@price, .@price * 2; break;
		}
	}

	mes "[Suhnbi]";
	if (.@weapon_lvl)
		mes "Você quer refinar um nível de sua"+ .@weapon_lvl +" arma?";
	mes "Para refinar isso, você precisará ter um ^ff9999"+ getitemname(.@item_req) +"^000000 e"+ .@price +" zeny.";
	mes "Você gostaria de continuar?";
	next;
	if(select("Sim Não") == 1) {
		if (getequippercentrefinery(.@part) < 100) {
			if (.@weapon_lvl) {
				mes "[Suhnbi]";
				mes "Uau !!";
				mes "Esta arma provavelmente";
				mes "parece que foi refinado ...";
				mes "muitas vezes ...";
				mes "Pode quebrar se";
				mes "você o refinar novamente.";
				next;
				mes "E se quebrar,";
				mes "você não pode mais usá-lo!";
				mes "Todas as cartas nele e as propriedades ^ff0000 serão perdidos ^000000!";
				mes "^ff0000Além disso, o equipamento irá quebrar! ^000000";
				mes "Tem certeza que ainda deseja continuar?";
				next;
				if(select("Sim não") == 2) {
					mes "[Suhnbi]";
					mes "Tudo bem.";
					mes "Porque se a arma quebra de um refinamento irracional, eu fico de mau humor também.";
					close;
				}
			} else {
				mes "[Suhnbi]";
				mes "Risadinha. Risadinha. Oh, você tem coragem, ousando refinar isso.";
				mes "Você sabe que é muito arriscado, não é?";
				next;
				mes "Se o seu equipamento de defesa estiver quebrado, você nunca mais poderá usá-lo.";
				mes "Até mesmo suas cartas e modificações ^ff0000 desaparecer completamente ^000000.";
				//mes "Tudo vai desaparecer. Como em ... SEI!";
				mes "Você realmente deseja continuar?";
				next;
				if(select("Sim não") == 2) {
					mes "[Suhnbi]";
					mes "Que absurdo. Você desperdiça meu precioso tempo.";
					mes "Cai fora, punk.";
					close;
				}
			}
		}
		if (countitem(.@item_req) > 0 && Zeny > .@price) {
			delitem .@item_req,1;
			set Zeny, Zeny - .@price;

			// anti-hack
			if (callfunc("F_IsEquipIDHack", .@part, getarg(4)) ||
				callfunc("F_IsEquipRefineHack", .@part, getarg(5)) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3])) {
				mes "[Holink]";
				emotion ET_FRET;
				mes "Espere um segundo ...";
				mes "Você me acha idiota ?!";
				mes "Você trocou o item enquanto eu não estava olhando! Saia daqui!";
				close;
			}

			return;
		}
		mes "[Suhnbi]";
		mes "Isso é tudo que você tem?";
		mes "Sinto muito, mas não posso fazer nada sem todos os materiais. Além disso, mereço alguns pagamentos pelo meu trabalho, não é?";
		close;
	}
	mes "[Suhnbi]";
	mes "Não posso evitar, mesmo que você não esteja feliz com isso ...";
	close;
}